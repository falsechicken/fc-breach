actor "Thompson" : RGUWeapon
{
  obituary "\cf%k \cc>> \cdThompson \cc>> \cf%o"
  inventory.pickupmessage "Thompson"  
  weapon.selectionorder 20
  weapon.ammotype "ThompsonClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "ThompsonAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 20
  -NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",2)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    THMA A 2 A_PlayWeaponSound("weapon/select")
    THMA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    THMA E 0 A_JumpIfNoAmmo("Reload")
    THMA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    THMA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    THMA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    THMA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THMA E 1 A_WeaponReady
    loop 
  Aiming:
    THMC D 0 A_JumpIfInventory("Reloading",1,"Reload")    
    THMC D 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    THMC D 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THMC D 1 A_WeaponReady(WRF_NOBOB)
    THMC D 0 A_ZoomFactor(1.2)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    THMH ABCD 2
    TNT1 A 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    THMA E 0 A_JumpIfInventory("Aiming",1,"FireAim")
    THMB A 0 A_JumpIfNoAmmo("ReloadStart")
    THMB A 0 A_FireCustomMissile("ThompsonPuff",0,0,4,1)
    THMB A 1 BRIGHT A_FireBullets (12.5, 12.5, -1, 65, "ThompsonBullet", 1, 3000)
    THMB B 0 ACS_ExecuteAlways(600,0,500,75*random(2,4),0)	//Recoil script
    THMB B 0 BRIGHT A_GunFlash
    THMB CD 1 A_JumpIfNoAmmo("FireEmpty")
    THMB A 0 A_JumpIfInventory("ThompsonAmmo",1,2)
    THMB A 0 A_ReFire
    goto InHand
    THMB A 0 A_JumpIfNoAmmo(2)
    THMB A 0 A_ReFire
    goto InHand
    THMB A 0 A_GiveInventory("ThompsonEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    THMB ABC 1 
    THMB D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+8
  FireAim:
    THMC D 0 A_JumpIfNoAmmo("ReloadStart")
    THMD A 0 A_FireCustomMissile("ThompsonPuff",0,0,0,5)
    THMD A 1 BRIGHT A_FireBullets (0.9, 0.9, -1, 65, "ThompsonBullet", 1, 5000)
    THMD B 0 ACS_ExecuteAlways(600,0,500,75*random(1,2),0)	//Recoil script
    THMD B 0 BRIGHT A_GunFlash
    THMD B 0 A_ZoomFactor(1.18)
    THMD CD 1 A_JumpIfNoAmmo("FireEmptyAim")
    THMD A 0 A_JumpIfInventory("ThompsonAmmo",1,2)
    THMD A 0 A_ReFire
    goto Aiming
    THMD A 0 A_JumpIfNoAmmo(2)
    THMD A 0 A_ReFire
    goto Aiming
    THMD A 0 A_GiveInventory("ThompsonEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    THMD ABC 1 
    THMD D 1 A_PlayWeaponSound("weapon/dryfire")
    goto FireAim+8
  Hold:
    THMA E 0 A_JumpIfInventory("Aiming",1,"HoldAim")
    THMA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    THMA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    THMC D 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("ThompsonAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    THME A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    THME A 0 A_WeaponReady(WRF_NOFIRE)
    THME A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THME B 0 A_WeaponReady(WRF_NOFIRE)
    THME B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THME C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    THME C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THME D 0 A_PlaySoundEx("thom/clipout","SoundSlot5")
    THME D 0 A_WeaponReady(WRF_NOFIRE)
    THME D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THME E 0 A_WeaponReady(WRF_NOFIRE)
    THME E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THME F 0 A_WeaponReady(WRF_NOFIRE)
    THME F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THME G 0 A_WeaponReady(WRF_NOFIRE)
    THME G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THME H 0 A_PlaySoundEx("thom/clipin","SoundSlot5")
    THME H 0 A_WeaponReady(WRF_NOFIRE)
    THME H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("ThompsonClip",1)
    TNT1 A 0 A_TakeInventory("ThompsonAmmo",1)
    TNT1 A 0 A_JumpIfInventory("ThompsonClip",20,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("ThompsonAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    THME I 0 A_WeaponReady(WRF_NOFIRE)
    THME I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THME J 0 A_WeaponReady(WRF_NOFIRE)
    THME J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_JumpIfInventory("ThompsonEmpty",1,"ReloadEndEmpty")
    THME K 0 A_WeaponReady(WRF_NOFIRE)
    THME K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  ReloadEndEmpty:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_TakeInventory("ThompsonEmpty",1)
    THMF A 0 A_WeaponReady(WRF_NOFIRE)
    THMF A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THMF B 0 A_WeaponReady(WRF_NOFIRE)
    THMF B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THMF C 0 A_PlaySoundEx("thom/bolt","SoundSlot5")
    THMF C 0 A_WeaponReady(WRF_NOFIRE)
    THMF C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THMF D 0 A_WeaponReady(WRF_NOFIRE)
    THMF D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    THMF E 0 A_WeaponReady(WRF_NOFIRE)
    THMF E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    THMC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    THMC A 0 A_ZoomFactor(1.2)		
    THMC ABCD 2
    THMC D 0 A_GiveInventory("Aiming",1)
    THMC D 0 ACS_ExecuteAlways(602,0,0,0,0)
    THMC D 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    THMC D 0 A_ZoomFactor(1.0)
    THMC DCBA 2
    THMC A 0 A_TakeInventory("Aiming",1)
    THMC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    THMC D 1 A_ZoomFactor(1.0)
    THMC DCBA 1
    THMC A 0 A_TakeInventory("Aiming",1)
    THMC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    THMG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    THMG B 3
    THMG C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    THMG DEFG 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGA A 0 A_JumpIfInventory("IsBritish",1,"MillsGrenade")
    FRGA ABCDEF 3
    FRGA G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGA H 3
    FRGA H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGA I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGA I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGA I 0 A_TakeInventory("KnifeAttack",1)
    FRGA I 3 A_FireCustomMissile("FragGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  MillsGrenade:
    FRGE ABCDEF 3
    FRGE G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGE H 3
    FRGE H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGE I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGE I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGE I 0 A_TakeInventory("KnifeAttack",1)
    FRGE I 3 A_FireCustomMissile("MillsBomb",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor ThompsonEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor ThompsonClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 20
    ammo.backpackamount 0
    ammo.backpackmaxamount 20
    +IGNORESKILL
}

actor ThompsonPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 1
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("thom/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("thom/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("thom/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("thom/fire4","SoundSlot6")
    stop
  }
}

actor ThompsonBullet : HHBulletPuff
{
  damagetype "Thompson"
}
