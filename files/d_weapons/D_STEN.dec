actor "Sten Gun" : RGUWeapon
{
  obituary "\cf%k \cc>> \cdSten Gun \cc>> \cf%o"
  inventory.pickupmessage "Sten Gun"  
  weapon.selectionorder 20
  weapon.ammotype "StenClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "StenAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 32
  -NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",2)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    STEA A 2 A_PlayWeaponSound("weapon/select")
    STEA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    STEA E 0 A_JumpIfNoAmmo("Reload")
    STEA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    STEA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    STEA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    STEA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEA E 1 A_WeaponReady
    loop 
  Aiming:
    STEC D 0 A_JumpIfInventory("Reloading",1,"Reload")    
    STEC D 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    STEC D 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEC D 1 A_WeaponReady(WRF_NOBOB)
    STEC D 0 A_ZoomFactor(1.2)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    STEH ABC 2
    TNT1 AA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    STEB D 1 A_JumpIfInventory("Aiming",1,"FireAim")
    STEB A 0 A_JumpIfNoAmmo("ReloadStart")
    STEB A 0 A_FireCustomMissile("StenPuff",0,0,6,-1)
    STEB A 1 BRIGHT A_FireBullets (8, 8, -1, 45, "StenBullet", 1, 3000)
    STEB B 0 ACS_ExecuteAlways(600,0,500,110*random(2,4),0)	//Recoil script
    STEB B 1 BRIGHT A_GunFlash
    STEB C 1 A_JumpIfNoAmmo("FireEmpty")
    STEB A 0 A_JumpIfInventory("StenAmmo",1,2)
    STEB A 0 A_ReFire
    goto InHand
    STEB A 0 A_JumpIfNoAmmo(2)
    STEB A 0 A_ReFire
    goto InHand
    STEB A 0 A_GiveInventory("StenEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    STEB ABC 1 
    STEB D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+7
  FireAim:
    STED D 1 A_JumpIfNoAmmo("ReloadStart")
    STED A 0 A_FireCustomMissile("StenPuff",0,0,0,6)
    STED A 1 BRIGHT A_FireBullets (0.9, 0.9, -1, 45, "StenBullet", 1, 5000)
    STED B 0 ACS_ExecuteAlways(600,0,500,110*random(1,2),0)	//Recoil script
    STED B 1 BRIGHT A_GunFlash
    STED B 0 A_ZoomFactor(1.18)
    STED C 1 A_JumpIfNoAmmo("FireEmptyAim")
    STED A 0 A_JumpIfInventory("StenAmmo",1,2)
    STED A 0 A_ReFire
    goto Aiming
    STED A 0 A_JumpIfNoAmmo(2)
    STED A 0 A_ReFire
    goto Aiming
    STED A 0 A_GiveInventory("StenEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    STED ABC 1 
    STED D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+11
  Hold:
    STEA E 0 A_JumpIfInventory("Aiming",1,"HoldAim")
    STEA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    STEA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    STEC D 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("StenAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    STEE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    STEE A 0 A_WeaponReady(WRF_NOFIRE)
    STEE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE B 0 A_WeaponReady(WRF_NOFIRE)
    STEE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE C 0 A_PlaySoundEx("sten/clipout","SoundSlot5")
    STEE C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    STEE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE D 0 A_WeaponReady(WRF_NOFIRE)
    STEE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE E 0 A_WeaponReady(WRF_NOFIRE)
    STEE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE F 0 A_WeaponReady(WRF_NOFIRE)
    STEE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE G 0 A_WeaponReady(WRF_NOFIRE)
    STEE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE H 0 A_WeaponReady(WRF_NOFIRE)
    STEE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE I 0 A_WeaponReady(WRF_NOFIRE)
    STEE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE J 0 A_WeaponReady(WRF_NOFIRE)
    STEE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE K 0 A_PlaySoundEx("sten/clipin","SoundSlot5")
    STEE K 0 A_WeaponReady(WRF_NOFIRE)
    STEE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE L 0 A_WeaponReady(WRF_NOFIRE)
    STEE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE M 0 A_WeaponReady(WRF_NOFIRE)
    STEE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("StenClip",1)
    TNT1 A 0 A_TakeInventory("StenAmmo",1)
    TNT1 A 0 A_JumpIfInventory("StenClip",32,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("StenAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_JumpIfInventory("StenEmpty",1,"ReloadEndEmpty")
    STEE N 0 A_WeaponReady(WRF_NOFIRE)
    STEE N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEE O 0 A_WeaponReady(WRF_NOFIRE)
    STEE O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  ReloadEndEmpty:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_TakeInventory("StenEmpty",1)
    STEF A 0 A_WeaponReady(WRF_NOFIRE)
    STEF A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEF B 0 A_PlaySoundEx("sten/bolt","SoundSlot5")
    STEF B 0 A_WeaponReady(WRF_NOFIRE)
    STEF B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEF C 0 A_WeaponReady(WRF_NOFIRE)
    STEF C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEF D 0 A_WeaponReady(WRF_NOFIRE)
    STEF D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    STEF E 0 A_WeaponReady(WRF_NOFIRE)
    STEF E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    STEC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    STEC A 0 A_ZoomFactor(1.2)		
    STEC ABCD 2
    STEC D 0 A_GiveInventory("Aiming",1)
    STEC D 0 ACS_ExecuteAlways(602,0,0,0,0)
    STEC D 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    STEC D 0 A_ZoomFactor(1.0)
    STEC DCBA 2
    STEC A 0 A_TakeInventory("Aiming",1)
    STEC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    STEC D 1 A_ZoomFactor(1.0)
    STEC DCBA 1
    STEC A 0 A_TakeInventory("Aiming",1)
    STEC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    STEG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    STEG B 3
    STEG C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    STEG DEFGH 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGE ABCDEF 3
    FRGE G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGE H 3
    FRGE H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGE I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGE I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGE I 0 A_TakeInventory("KnifeAttack",1)
    FRGE I 3 A_FireCustomMissile("MillsBomb",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor StenEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor StenClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 32
    ammo.backpackamount 0
    ammo.backpackmaxamount 32
    +IGNORESKILL
}

actor StenPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 1
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("sten/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("sten/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("sten/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("sten/fire4","SoundSlot6")
    stop
  }
}

actor StenBullet : HHBulletPuff
{
  damagetype "Sten"
}
