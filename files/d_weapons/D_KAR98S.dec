actor "Scoped Kar98k" : RGUWeapon
{
  obituary "\cf%k \cc>> \cgScoped Kar98k \cc>> \cf%o"
  inventory.pickupmessage "Scoped Kar98k"  
  weapon.selectionorder 16
  weapon.ammotype "Kar98KClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "Kar98KAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 5
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",4)	//Determines player speed - 4 means 30% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    S98A A 2 A_PlayWeaponSound("weapon/select")
    S98A BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    S98A E 0 A_JumpIfNoAmmo("Reload")
    S98A E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    S98A E 0 A_JumpIfInventory("Reloading",1,"Reload")
    S98A E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    S98A E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98A E 0 A_JumpIfInventory("Kar98kSClosed",1,1)
    S98A E 1 A_WeaponReady
    loop 
  Aiming:
    SCOP B 0 A_JumpIfInventory("Reloading",1,"Reload")    
    SCOP B 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    SCOP B 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SCOP B 1 A_WeaponReady(WRF_NOBOB)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 4 means 30% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
    S98H ABCD 2
    TNT1 A 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    S98A E 0 A_JumpIfInventory("Reloading",1,"ReloadEnd")
    S98A E 1 A_JumpIfInventory("Aiming",1,"FireAim")
    S98A E 1 A_JumpIfInventory("Kar98kSBolt",1,"Bolt")
    S98B A 0 A_JumpIfNoAmmo("ReloadStart")
    S98B A 0 A_TakeInventory("Kar98kSClosed",1)
    S98B A 0 A_FireCustomMissile("Kar98KPuff2",0,0,4,2)
    S98B A 1 BRIGHT A_FireBullets (15, 15, -1, 100, "Kar98KBullet3", 1, 3000)
    S98B B 0 ACS_ExecuteAlways(600,0,1500,150*random(2,4),0)		//Recoil script
    S98B B 1 BRIGHT A_GunFlash
    S98B CDEF 1 A_JumpIfNoAmmo("FireEmpty")
    S98B F 5 A_WeaponReady(WRF_NOFIRE)
    S98B F 0 A_GiveInventory("Kar98kSBolt",1)
    S98B F 5 A_WeaponReady(WRF_NOFIRE)
    goto InHand
    S98B A 0 A_JumpIfNoAmmo(2)
    goto InHand
    S98B A 0 A_GiveInventory("Kar98KEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    S98B CDEF 1
    goto Fire+17
  FireAim:
    SCOP B 1 A_JumpIfInventory("Kar98kSBolt",1,"BoltAim") 
    SCOP B 1 A_JumpIfNoAmmo("ReloadStart")
    SCOP B 0 A_TakeInventory("Kar98kSClosed",1)
    SCOP B 0 A_FireCustomMissile("Kar98KPuff2",0,0,0,4)
    SCOP B 1 BRIGHT A_FireBullets (0.1, 0.1, 2, 100, "Kar98KBullet2", 1, 9001) //45, 90, 135, 180, 225, 270
    SCOP B 0 ACS_ExecuteAlways(600,0,1500,150*random(1,2),0)		//Recoil script
    SCOP B 1 BRIGHT A_GunFlash
    SCOP B 4 A_ZoomFactor(1.5)
    SCOP BBBB 1 A_JumpIfNoAmmo("FireEmptyAim")
    SCOP B 5 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB|WRF_NOFIRE)
    SCOP B 0 A_ZoomFactor(3.5)
	SCOP B 0 A_GiveInventory("Kar98kSBolt",1)
    SCOP A 0 A_JumpIfInventory("Kar98KAmmo",1,2)
    SCOP A 0 A_ReFire
    goto InHand
    SCOP B 0 A_JumpIfNoAmmo(2)
    goto InHand
    SCOP B 0 A_GiveInventory("Kar98KEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    SCOP BBBB 1
    goto Fire+17
  FireAimStill:
    SCOP B 1 BRIGHT A_FireBullets (0.1, 0.1, 2, 100, "Kar98KBullet2", 1, 9001) //45, 90, 135, 180, 225, 270
    goto FireAim+5
  Hold:
    S98A E 0 A_JumpIfInventory("Aiming",1,"HoldAim")	//Don't need a hold because of the already long rechamber time
    S98A E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    S98A E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    K98C E 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("Kar98KAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    S98E A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    S98E N 0 A_GiveInventory("Reloading",1)
    S98E A 0 A_WeaponReady(WRF_NOFIRE)
    S98E A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E B 0 A_TakeInventory("Kar98kSClosed",1)
    S98E B 0 A_PlaySoundEx("kar98k/open","SoundSlot5")
    S98E B 0 A_WeaponReady(WRF_NOFIRE)
    S98E B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    S98E C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E D 0 A_WeaponReady(WRF_NOFIRE)
    S98E D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E E 0 A_WeaponReady(WRF_NOFIRE)
    S98E E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E F 0 A_WeaponReady(WRF_NOFIRE)
    S98E F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E G 0 A_WeaponReady(WRF_NOFIRE)
    S98E G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E H 0 A_WeaponReady(WRF_NOFIRE)
    S98E H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("Kar98KClip",1)
    TNT1 A 0 A_TakeInventory("Kar98KAmmo",1)
    TNT1 A 0 A_TakeInventory("Kar98kSBolt",1)
    S98E I 0 A_WeaponReady(WRF_NOFIRE)
    S98E I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E J 0 A_PlaySoundEx("spring/shell","SoundSlot5")
    S98E J 0 A_WeaponReady(WRF_NOBOB)
    S98E J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E K 0 A_WeaponReady(WRF_NOBOB)
    S98E K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_JumpIfInventory("Kar98KClip",5,"ReloadEnd")
    S98E F 0 A_WeaponReady(WRF_NOBOB)
    S98E F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E G 0 A_WeaponReady(WRF_NOBOB)
    S98E G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E H 0 A_WeaponReady(WRF_NOBOB)
    S98E H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_JumpIfInventory("Kar98KAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    S98E L 0 A_WeaponReady(WRF_NOFIRE)
    S98E L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E M 0 A_WeaponReady(WRF_NOFIRE)
    S98E M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    S98E N 0 A_GiveInventory("Kar98kSClosed",1)
    S98E N 0 A_PlaySoundEx("kar98k/close","SoundSlot5")
    S98E N 0 A_WeaponReady(WRF_NOFIRE)
    S98E N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto InHand
  AltFire:	//Aim
    S98A E 0 A_JumpIfInventory("Reloading",1,"ReloadEnd")
    S98C A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    S98C ABC 2
    S98C A 0 A_ZoomFactor(3.5,ZOOM_INSTANT)
    S98C E 0 A_GiveInventory("Aiming",1)
    S98C E 0 ACS_ExecuteAlways(602,0,1,0,0)
    S98C E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    S98C C 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
    S98C CBA 2
    S98C A 0 A_TakeInventory("Aiming",1)
    S98C A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    S98C C 1 A_ZoomFactor(1.0,ZOOM_INSTANT)
    S98C CBA 1
    S98C A 0 A_TakeInventory("Aiming",1)
    S98C A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    S98G A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    S98G B 3
    S98G C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    S98G DE 3
    TNT1 A 3
    S98G FG 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGC ABCDEFG 3
    TNT1 A 15 
    FRGC H 3
    FRGC H 0 A_PlaySoundEx("stick/throw","SoundSlot5")
    FRGC I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGC I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGC I 0 A_TakeInventory("KnifeAttack",1)
    FRGC I 3 A_FireCustomMissile("StickGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  Bolt:
	S98B H 0 A_JumpIfInventory("Aiming",1,"BoltAim")
	S98B H 0 A_GiveInventory("Kar98kSClosed",1)
	S98B H 0 A_TakeInventory("Kar98kSBolt",1)
    S98B H 5 A_PlayWeaponSound("kar98k/chamber")
    S98B IJKL 5 A_WeaponReady(WRF_NOFIRE)
	goto InHand
  BoltAim:
    S98C C 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
    S98C CBA 2
    S98C A 0 A_TakeInventory("Aiming",1)
    S98C A 0 ACS_ExecuteAlways(602,0,0,0,0)
    S98C H 0 A_GiveInventory("Kar98kSClosed",1)
    S98C H 0 A_TakeInventory("Kar98kSBolt",1)
    S98B H 5 A_PlayWeaponSound("kar98k/chamber")
    S98B IJKL 5 A_WeaponReady(WRF_NOFIRE)
	goto InHand
  }
}

actor Kar98KPuff2
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuffShotgun_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash2", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("Kar98K/sniper1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("Kar98K/sniper3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("Kar98K/sniper2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("Kar98K/sniper4","SoundSlot6")
    stop
  }
}

actor Kar98KBullet2 : HHBulletPuff
{
  damagetype "Kar982"
}

actor Kar98kSBolt : Inventory //If this is in the players inventory the weapon needs to be bolted.
{
	inventory.amount 0
	inventory.maxamount 1
}

actor Kar98kSClosed : Inventory //Custom closed actor to prevent conflict with the Kar98K
{
	inventory.amount 0
	inventory.maxamount 1
}
