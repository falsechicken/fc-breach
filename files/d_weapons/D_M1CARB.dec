actor "M1A1 Carbine" : RGUWeapon
{
  obituary "\cf%k \cc>> \csM1A1 Carbine \cc>> \cf%o"
  inventory.pickupmessage "M1A1 Carbine"  
  weapon.selectionorder 19
  weapon.ammotype "M1CarbineClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "M1CarbineAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 15
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",3)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    M1CA A 2 A_PlayWeaponSound("weapon/select")
    M1CA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    M1CA E 0 A_JumpIfNoAmmo("Reload")
    M1CA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    M1CA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    M1CA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M1CA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CA E 1 A_WeaponReady
    loop 
  Aiming:
    M1CC E 0 A_JumpIfInventory("Reloading",1,"Reload")    
    M1CC E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M1CC E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CC E 1 A_WeaponReady(WRF_NOBOB)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    M1CH ABC 2
    TNT1 AA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    M1CA E 1 A_JumpIfInventory("Aiming",1,"FireAim")
    M1CB A 0 A_JumpIfNoAmmo("ReloadStart")
    M1CB A 0 A_FireCustomMissile("M1CarbinePuff",0,0,4,2)
    M1CB A 1 BRIGHT A_FireBullets (12, 12, -1, 60, "M1CarbineBullet", 1, 3000)
    M1CB B 0 ACS_ExecuteAlways(600,0,750,100*random(2,4),0)		//Recoil script
    M1CB B 1 BRIGHT A_GunFlash
    M1CB C 1 A_JumpIfNoAmmo("FireEmpty")
    M1CB D 1 A_WeaponReady
    M1CA EEEE 1 A_WeaponReady
    M1CB A 0 A_JumpIfInventory("M1CarbineAmmo",1,2)
    M1CB A 0 A_ReFire
    goto InHand
    M1CB A 0 A_JumpIfNoAmmo(2)
    M1CB A 0 A_ReFire
    goto InHand
    M1CB A 0 A_GiveInventory("M1CarbineEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    M1CB CD 1
    goto Fire+12
  FireAim:
    M1CC E 1 A_JumpIfNoAmmo("ReloadStart")
    M1CD A 0 A_FireCustomMissile("M1CarbinePuff",0,0,0,4)
    M1CD A 1 BRIGHT A_FireBullets (0.75, 0.75, -1, 75, "M1CarbineBullet", 1, 6000)
    M1CD B 0 ACS_ExecuteAlways(600,0,750,100*random(1,2),0)			//Recoil script
    M1CD B 1 BRIGHT A_GunFlash
    M1CD B 1 A_ZoomFactor(1.18)
    M1CD C 1 A_JumpIfNoAmmo("FireEmptyAim")
    M1CD D 1 A_WeaponReady
    M1CC EEEE 1 A_WeaponReady
    M1CC E 0 A_ZoomFactor(1.2)
    M1CD A 0 A_JumpIfInventory("M1CarbineAmmo",1,2)
    M1CD A 0 A_ReFire
    goto InHand
    M1CD A 0 A_JumpIfNoAmmo(2)
    M1CD A 0 A_ReFire
    goto InHand
    M1CD A 0 A_GiveInventory("M1CarbineEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    M1CD CD 1
    goto Fire+12
  Hold:
    M1CA E 3 A_JumpIfInventory("Aiming",1,"HoldAim")
    M1CA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M1CA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CA E 0 A_JumpIfInventory("IsBot",1,"Fire")
    M1CA E 1 A_ReFire
    goto InHand
  HoldAim:
    M1CC E 3
    M1CC E 0 A_JumpIfInventory("IsBot",1,"FireAim")
    M1CC E 1 A_ReFire
    goto Aiming
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("M1CarbineAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    M1CE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    M1CE A 0 A_WeaponReady(WRF_NOFIRE)
    M1CE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE B 0 A_WeaponReady(WRF_NOFIRE)
    M1CE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    M1CE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE D 0 A_WeaponReady(WRF_NOFIRE)
    M1CE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE E 0 A_PlaySoundEx("carbine/clipout","SoundSlot5")
    M1CE E 0 A_WeaponReady(WRF_NOFIRE)
    M1CE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE F 0 A_WeaponReady(WRF_NOFIRE)
    M1CE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE G 0 A_WeaponReady(WRF_NOFIRE)
    M1CE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE H 0 A_WeaponReady(WRF_NOFIRE)
    M1CE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE I 0 A_WeaponReady(WRF_NOFIRE)
    M1CE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE J 0 A_PlaySoundEx("carbine/clipin","SoundSlot5")
    M1CE J 0 A_WeaponReady(WRF_NOFIRE)
    M1CE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE K 0 A_WeaponReady(WRF_NOFIRE)
    M1CE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE L 0 A_WeaponReady(WRF_NOFIRE)
    M1CE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE M 0 A_WeaponReady(WRF_NOFIRE)
    M1CE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE N 0 A_WeaponReady(WRF_NOFIRE)
    M1CE N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE O 0 A_PlaySoundEx("carbine/hit","SoundSlot5")
    M1CE O 0 A_WeaponReady(WRF_NOFIRE)
    M1CE O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("M1CarbineClip",1)
    TNT1 A 0 A_TakeInventory("M1CarbineAmmo",1)
    TNT1 A 0 A_JumpIfInventory("M1CarbineClip",15,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("M1CarbineAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    M1CE A 0 A_JumpIfInventory("M1CarbineEmpty",1,"ReloadEndEmpty")
    M1CE P 0 A_WeaponReady(WRF_NOFIRE)
    M1CE P 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE Q 0 A_WeaponReady(WRF_NOFIRE)
    M1CE Q 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CE R 0 A_WeaponReady(WRF_NOFIRE)
    M1CE R 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  ReloadEndEmpty:
    TNT1 A 0 A_TakeInventory("M1CarbineEmpty",1)
    M1CF A 0 A_WeaponReady(WRF_NOFIRE)
    M1CF A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CF B 0 A_WeaponReady(WRF_NOFIRE)
    M1CF B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CF C 0 A_WeaponReady(WRF_NOFIRE)
    M1CF C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CF D 0 A_WeaponReady(WRF_NOFIRE)
    M1CF D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CF E 0 A_PlaySoundEx("carbine/bolt","SoundSlot5")
    M1CF E 0 A_WeaponReady(WRF_NOFIRE)
    M1CF E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CF F 0 A_WeaponReady(WRF_NOFIRE)
    M1CF F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1CF G 0 A_WeaponReady(WRF_NOFIRE)
    M1CF G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    M1CC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    M1CC A 0 A_ZoomFactor(1.2)		
    M1CC ABCDE 2
    M1CC E 0 A_GiveInventory("Aiming",1)
    M1CC E 0 ACS_ExecuteAlways(602,0,0,0,0)
    M1CC E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    M1CC E 0 A_ZoomFactor(1.0)
    M1CC EDCBA 2
    M1CC A 0 A_TakeInventory("Aiming",1)
    M1CC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    M1CC E 1 A_ZoomFactor(1.0)
    M1CC EDCBA 1
    M1CC A 0 A_TakeInventory("Aiming",1)
    M1CC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    M1CG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    M1CG B 3
    M1CG C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    M1CG DE 3
    TNT1 A 3
    M1CG FG 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGA ABCDEF 3
    FRGA G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGA H 3
    FRGA H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGA I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGA I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGA I 0 A_TakeInventory("KnifeAttack",1)
    FRGA I 3 A_FireCustomMissile("FragGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor M1CarbineEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor M1CarbineClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 15
    ammo.backpackamount 0
    ammo.backpackmaxamount 15
    +IGNORESKILL
}

actor M1CarbinePuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("carbine/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("carbine/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("carbine/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("carbine/fire4","SoundSlot6")
    stop
  }
}

actor M1CarbineBullet : HHBulletPuff
{
  damagetype "M1Carbine"
}
