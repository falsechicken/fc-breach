actor "Colt .45" : RGUWeapon
{
  obituary "\cf%k \cc>> \ciColt .45 \cc>> \cf%o"
  inventory.pickupmessage "Colt .45"  
  weapon.selectionorder 21
  weapon.ammotype "Colt45Clip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "Colt45Ammo"
  weapon.ammouse2 0
  weapon.ammogive2 7
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",1)	//Determines player speed - 1 means normal speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    CLTA A 2 A_PlayWeaponSound("weapon/select")
    CLTA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    CLTA E 0 A_JumpIfNoAmmo("Reload")
    CLTA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    CLTA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    CLTA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    CLTA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTA E 1 A_WeaponReady
    loop 
  Aiming:
    CLTC D 0 A_JumpIfInventory("Reloading",1,"Reload")    
    CLTC D 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    CLTC D 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTC D 1 A_WeaponReady(WRF_NOBOB)
    CLTC D 0 A_ZoomFactor(1.0)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 1 means normal speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    CLTA EDCBA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    CLTA E 0 A_JumpIfInventory("Aiming",1,"FireAim")
    CLTA E 1 A_JumpIfNoAmmo("ReloadStart")
    CLTB A 0 A_FireCustomMissile("Colt45Puff",0,0,12,1)
    CLTB A 1 BRIGHT A_FireBullets (6, 6, -1, 50, "Colt45Bullet", 1, 2300)
    CLTB B 0 ACS_ExecuteAlways(600,0,600,40*random(2,4),0)		//Recoil script
    CLTB B 1 BRIGHT A_GunFlash
    CLTB B 0 A_JumpIfNoAmmo("FireEmpty")
    CLTB CDEFGH 1 A_WeaponReady
    CLTB A 0 A_JumpIfInventory("Colt45Ammo",1,2)
    CLTB A 0 A_ReFire
    goto InHand
    CLTB A 0 A_JumpIfNoAmmo(2)
    CLTB A 0 A_ReFire
    goto InHand
    CLTB A 0 A_GiveInventory("Colt45Empty",1)		//Go to reload
    goto Reload
  FireEmpty:
    CLTB CDEFG 1 
    CLTB H 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+13
  FireAim:
    CLTC D 1 A_JumpIfNoAmmo("ReloadStart")
    CLTD A 0 A_FireCustomMissile("Colt45Puff",0,0,0,6)
    CLTD A 1 BRIGHT A_FireBullets (0.5, 0.5, -1, 50, "Colt45Bullet", 1, 2300)
    CLTD B 0 ACS_ExecuteAlways(600,0,600,40*random(1,2),0)		//Recoil script
    CLTD B 1 BRIGHT A_GunFlash
    CLTD B 1 A_ZoomFactor(0.98)
    CLTD B 0 A_JumpIfNoAmmo("FireEmptyAim")
    CLTD CDEFGH 1 A_WeaponReady
    CLTD A 0 A_JumpIfInventory("Colt45Ammo",1,2)
    CLTD A 0 A_ReFire
    goto Aiming
    CLTD A 0 A_JumpIfNoAmmo(2)
    CLTD A 0 A_ReFire
    goto Aiming
    CLTD A 0 A_GiveInventory("Colt45Empty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    CLTD CDEFG 1 
    CLTD H 1 A_PlayWeaponSound("weapon/dryfire")
    goto FireAim+13
  Hold:
    CLTA E 3 A_JumpIfInventory("Aiming",1,"HoldAim")
    CLTA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    CLTA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTA E 0 A_JumpIfInventory("IsBot",1,"Fire")
    CLTA E 1 A_ReFire
    goto InHand
  HoldAim:
    CLTC D 3
    CLTC D 0 A_JumpIfInventory("IsBot",1,"FireAim")
    CLTC D 1 A_ReFire
    goto Aiming
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("Colt45Ammo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    CLTE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    CLTE A 0 A_JumpIfInventory("Colt45Empty",1,"ReloadStartEmpty")
    CLTE A 0 A_WeaponReady(WRF_NOFIRE)
    CLTE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTE B 0 A_PlaySoundEx("colt45/clipout","SoundSlot5")
    CLTE B 0 A_WeaponReady(WRF_NOFIRE)
    CLTE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTE C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    CLTE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTE D 0 A_WeaponReady(WRF_NOFIRE)
    CLTE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTE E 0 A_WeaponReady(WRF_NOFIRE)
    CLTE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTE F 0 A_WeaponReady(WRF_NOFIRE)
    CLTE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTE G 0 A_PlaySoundEx("colt45/clipin","SoundSlot5")
    CLTE G 0 A_WeaponReady(WRF_NOFIRE)
    CLTE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTE H 0 A_WeaponReady(WRF_NOFIRE)
    CLTE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTE I 0 A_WeaponReady(WRF_NOFIRE)
    CLTE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTE J 0 A_WeaponReady(WRF_NOFIRE)
    CLTE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTE K 0 A_WeaponReady(WRF_NOFIRE)
    CLTE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("Colt45Clip",1)
    TNT1 A 0 A_TakeInventory("Colt45Ammo",1)
    TNT1 A 0 A_JumpIfInventory("Colt45Clip",7,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("Colt45Ammo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    CLTE L 0 A_WeaponReady(WRF_NOFIRE)
    CLTE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTE M 0 A_WeaponReady(WRF_NOFIRE)
    CLTE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  ReloadStartEmpty:
    CLTF A 0 A_WeaponReady(WRF_NOFIRE)
    CLTF A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF B 0 A_PlaySoundEx("colt45/clipout","SoundSlot5")
    CLTF B 0 A_WeaponReady(WRF_NOFIRE)
    CLTF B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    CLTF C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF D 0 A_WeaponReady(WRF_NOFIRE)
    CLTF D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF E 0 A_WeaponReady(WRF_NOFIRE)
    CLTF E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF F 0 A_WeaponReady(WRF_NOFIRE)
    CLTF F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF G 0 A_PlaySoundEx("colt45/clipin","SoundSlot5")
    CLTF G 0 A_WeaponReady(WRF_NOFIRE)
    CLTF G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF H 0 A_WeaponReady(WRF_NOFIRE)
    CLTF H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF I 0 A_WeaponReady(WRF_NOFIRE)
    CLTF I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF J 0 A_WeaponReady(WRF_NOFIRE)
    CLTF J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF K 0 A_WeaponReady(WRF_NOFIRE)
    CLTF K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF L 0 A_WeaponReady(WRF_NOFIRE)
    CLTF L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmoEmpty
  ReloadAmmoEmpty:
    TNT1 A 0 A_GiveInventory("Colt45Clip",1)
    TNT1 A 0 A_TakeInventory("Colt45Ammo",1)
    TNT1 A 0 A_JumpIfInventory("Colt45Clip",7,"ReloadEndEmpty")
    TNT1 A 0 A_JumpIfInventory("Colt45Ammo",1,"ReloadAmmoEmpty")
    goto ReloadEndEmpty
  ReloadEndEmpty:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_TakeInventory("Colt45Empty",1)
    CLTF M 0 A_WeaponReady(WRF_NOFIRE)
    CLTF M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF N 0 A_PlaySoundEx("colt45/slide","SoundSlot5")
    CLTF N 0 A_WeaponReady(WRF_NOFIRE)
    CLTF N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    CLTF O 0 A_WeaponReady(WRF_NOFIRE)
    CLTF O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    CLTC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    CLTC A 0 A_ZoomFactor(1.0)		//No FOV change for handguns
    CLTC ABCD 2
    CLTC D 0 A_GiveInventory("Aiming",1)
    CLTC D 0 ACS_ExecuteAlways(602,0,0,0,0)
    CLTC D 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    CLTC D 0 A_ZoomFactor(1.0)
    CLTC DCBA 2
    CLTC A 0 A_TakeInventory("Aiming",1)
    CLTC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    CLTC D 1 A_ZoomFactor(1.0)
    CLTC DCBA 1
    CLTC A 0 A_TakeInventory("Aiming",1)
    CLTC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,1)		//Recoil script
    CLTG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    CLTG BC 3
    CLTG D 3 A_FireCustomMissile("MeleeAttack",0,0,0,0)
    TNT1 AA 3
    CLTG EF 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGA ABCDEF 3
    FRGA G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGA H 3
    FRGA H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGA I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGA I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGA I 0 A_TakeInventory("KnifeAttack",1)
    FRGA I 3 A_FireCustomMissile("FragGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor Colt45Empty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor Colt45Clip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 7
    ammo.backpackamount 0
    ammo.backpackmaxamount 7
    +IGNORESKILL
}

actor Colt45Puff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 1
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_PlaySoundEx("colt45/fire","SoundSlot5")
    TNT1 A 0 A_PlaySoundEx("colt45/firex","SoundSlot6")
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 5 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    stop
  }
}

actor Colt45Bullet : HHBulletPuff
{
  damagetype "Colt"
}
