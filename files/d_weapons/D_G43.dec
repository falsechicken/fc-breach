actor "Gewehr 43" : RGUWeapon
{
  obituary "\cf%k \cc>> \crGewehr 43 \cc>> \cf%o"
  inventory.pickupmessage "Gewehr 43"  
  weapon.selectionorder 18
  weapon.ammotype "G43Clip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "G43Ammo"
  weapon.ammouse2 0
  weapon.ammogive2 10
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",3)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    G43A A 2 A_PlayWeaponSound("weapon/select")
    G43A BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    G43A E 0 A_JumpIfNoAmmo("Reload")
    G43A E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    G43A E 0 A_JumpIfInventory("Reloading",1,"Reload")
    G43A E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    G43A E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43A E 1 A_WeaponReady
    loop 
  Aiming:
    G43C E 0 A_JumpIfInventory("Reloading",1,"Reload")    
    G43C E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    G43C E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43C E 1 A_WeaponReady(WRF_NOBOB)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    G43H ABCD 2
    TNT1 A 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    G43A E 1 A_JumpIfInventory("Aiming",1,"FireAim")
    G43B A 0 A_JumpIfNoAmmo("ReloadStart")
    G43B A 0 A_FireCustomMissile("G43Puff",0,0,4,2)
    G43B A 1 BRIGHT A_FireBullets (12, 12, -1, 65, "G43Bullet", 1, 3000)
    G43B B 0 ACS_ExecuteAlways(600,0,1000,100*random(2,3),0)		//Recoil script
    G43B B 1 BRIGHT A_GunFlash
    G43B C 1 A_JumpIfNoAmmo("FireEmpty")
    G43B DE 1 A_WeaponReady
    G43A EEEE 1 A_WeaponReady
    G43B A 0 A_JumpIfInventory("G43Ammo",1,2)
    G43B A 0 A_ReFire
    goto InHand
    G43B A 0 A_JumpIfNoAmmo(2)
    G43B A 0 A_ReFire
    goto InHand
    G43B A 0 A_GiveInventory("G43Empty",1)		//Go to reload
    goto Reload
  FireEmpty:
    G43B CDE 1
    goto Fire+13
  FireAim:
    G43C E 1 A_JumpIfNoAmmo("ReloadStart")
    G43D A 0 A_FireCustomMissile("G43Puff",0,0,0,4)
    G43D A 1 BRIGHT A_FireBullets (0.75, 0.75, -1, 65, "G43Bullet", 1, 6000)
    G43D B 0 ACS_ExecuteAlways(600,0,1000,100*random(1,2),0)		//Recoil script
    G43D B 1 BRIGHT A_GunFlash
    G43D B 1 A_ZoomFactor(1.18)
    G43D C 1 A_JumpIfNoAmmo("FireEmptyAim")
    G43D DE 1 A_WeaponReady
    G43C EEEE 1 A_WeaponReady
    G43D E 0 A_ZoomFactor(1.2)
    G43D A 0 A_JumpIfInventory("G43Ammo",1,2)
    G43D A 0 A_ReFire
    goto InHand
    G43D A 0 A_JumpIfNoAmmo(2)
    G43D A 0 A_ReFire
    goto InHand
    G43D A 0 A_GiveInventory("G43Empty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    G43D CDE 1
    goto Fire+13
  Hold:
    G43A E 3 A_JumpIfInventory("Aiming",1,"HoldAim")
    G43A E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    G43A E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43A E 0 A_JumpIfInventory("IsBot",1,"Fire")
    G43A E 1 A_ReFire
    goto InHand
  HoldAim:
    G43C E 3
    G43C E 0 A_JumpIfInventory("IsBot",1,"FireAim")
    G43C E 1 A_ReFire
    goto Aiming
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("G43Ammo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    G43E A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    G43E A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E B 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E C 0 A_PlaySoundEx("g43/clipout","SoundSlot5")
    G43E C 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)         //This apparent "messy" setup allows for interruption at all times
    G43E C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E D 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E E 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E F 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E G 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E H 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E I 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E J 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E K 0 A_PlaySoundEx("g43/clipin","SoundSlot5")
    G43E K 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E L 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E M 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E N 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E O 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("G43Clip",1)
    TNT1 A 0 A_TakeInventory("G43Ammo",1)
    TNT1 A 0 A_JumpIfInventory("G43Clip",10,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("G43Ammo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    G43E P 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E P 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E Q 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E Q 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    G43E R 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    G43E R 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    G43C A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    G43C A 0 A_ZoomFactor(1.2)		
    G43C ABCDE 2
    G43C E 0 A_GiveInventory("Aiming",1)
    G43C E 0 ACS_ExecuteAlways(602,0,0,0,0)
    G43C E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    G43C E 0 A_ZoomFactor(1.0)
    G43C EDCBA 2
    G43C A 0 A_TakeInventory("Aiming",1)
    G43C A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    G43C E 1 A_ZoomFactor(1.0)
    G43C EDCBA 1
    G43C A 0 A_TakeInventory("Aiming",1)
    G43C A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    G43G A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    G43G B 3
    G43G C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    G43G DEFG 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGC ABCDEFG 3
    TNT1 A 15 
    FRGC H 3
    FRGC H 0 A_PlaySoundEx("stick/throw","SoundSlot5")
    FRGC I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGC I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGC I 0 A_TakeInventory("KnifeAttack",1)
    FRGC I 3 A_FireCustomMissile("StickGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor G43Empty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor G43Clip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 10
    ammo.backpackamount 0
    ammo.backpackmaxamount 10
    +IGNORESKILL
}

actor G43Puff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("g43/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("g43/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("g43/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("g43/fire4","SoundSlot6")
    stop
  }
}

actor G43Bullet : HHBulletPuff
{
  damagetype "G43"
}
