actor "Bren Gun" : RGUWeapon
{
  obituary "\cf%k \cc>> \cqBren Gun \cc>> \cf%o"
  inventory.pickupmessage "Bren"  
  weapon.selectionorder 15
  weapon.ammotype "BrenClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "BrenAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 30
  -NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",5)	//Determines player speed - 5 means 40% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    BREA A 2 A_PlayWeaponSound("weapon/select")
    BREA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    BREA E 0 A_JumpIfNoAmmo("Reload")
    BREA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    BREA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    BREA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    BREA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREA E 1 A_WeaponReady
    loop 
  Aiming:
    BREC E 0 A_JumpIfInventory("Reloading",1,"Reload")    
    BREC E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    BREC E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREC E 1 A_WeaponReady(WRF_NOBOB)
    BREC E 0 A_ZoomFactor(1.3)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 5 means 40% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    BREA EDCBA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    BREA E 1 A_JumpIfInventory("Aiming",1,"FireAim")
    BREB A 0 A_JumpIfNoAmmo("ReloadStart")
    BREB A 0 A_FireCustomMissile("BrenPuff",0,0,4,1)
    BREB A 1 BRIGHT A_FireBullets (25, 25, -1, 75, "BrenBullet", 1, 3000)
    BREB B 0 ACS_ExecuteAlways(600,0,900,150*random(2,4),0)		//Recoil script
    BREB B 1 BRIGHT A_GunFlash
    BREB CD 1 A_JumpIfNoAmmo("FireEmpty")
    BREB A 0 A_JumpIfInventory("BrenAmmo",1,2)
    BREB A 0 A_ReFire
    goto InHand
    BREB A 0 A_JumpIfNoAmmo(2)
    BREB A 0 A_ReFire
    goto InHand
    BREB A 0 A_GiveInventory("BrenEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    BREB ABC 1 
    BREB D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+8
  FireAim:
    BREC E 1 A_JumpIfNoAmmo("ReloadStart")
    BRED A 0 A_FireCustomMissile("BrenPuff",0,0,0,6)
    BRED A 1 BRIGHT A_FireBullets (0.35, 0.35, -1, 75, "BrenBullet", 1, 6000)
    BRED B 0 ACS_ExecuteAlways(600,0,900,150*random(1,3),0)		//Recoil script
    BRED B 1 BRIGHT A_GunFlash
    BRED B 0 A_ZoomFactor(1.28)
    BRED CD 1 A_JumpIfNoAmmo("FireEmptyAim")
    BRED A 0 A_JumpIfInventory("BrenAmmo",1,2)
    BRED A 0 A_ReFire
    goto Aiming
    BRED A 0 A_JumpIfNoAmmo(2)
    BRED A 0 A_ReFire
    goto Aiming
    BRED A 0 A_GiveInventory("BrenEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    BRED ABC 1 
    BRED D 1 A_PlayWeaponSound("weapon/dryfire")
    goto FireAim+8
  Hold:
    BREA E 0 A_JumpIfInventory("Aiming",1,"HoldAim")
    BREA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    BREA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    BREC D 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("BrenAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    BREE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    BREE A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE B 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE C 0 A_JumpIfInventory("BrenClip",1,8)
    BREE C 0 A_PlaySoundEx("bren/bolt","SoundSlot5")
    BREE C 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)			//This apparent "messy" setup allows for interruption at all times
    BREE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE D 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE E 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE F 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE G 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE H 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE I 0 A_PlaySoundEx("bren/clipout","SoundSlot5")
    BREE I 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE J 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREF A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREF A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE K 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE L 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE M 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE N 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE O 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE P 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE P 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREF B 0 A_PlaySoundEx("bren/clipin","SoundSlot5")
    BREF B 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREF B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE Q 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE Q 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("BrenClip",1)
    TNT1 A 0 A_TakeInventory("BrenAmmo",1)
    TNT1 A 0 A_JumpIfInventory("BrenClip",30,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("BrenAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    BREE R 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE R 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE S 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE S 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE T 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE T 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE U 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE U 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BREE V 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BREE V 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto InHand
  AltFire:	//Aim
    BREC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    BREC A 0 A_ZoomFactor(1.3)		
    BREC ABCDE 2
    BREC E 0 A_GiveInventory("Aiming",1)
    BREC E 0 ACS_ExecuteAlways(602,0,0,0,0)
    BREC E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    BREC E 0 A_ZoomFactor(1.0)
    BREC EDCBA 2
    BREC A 0 A_TakeInventory("Aiming",1)
    BREC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    BREC E 1 A_ZoomFactor(1.0)
    BREC EDCBA 1
    BREC A 0 A_TakeInventory("Aiming",1)
    BREC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,1500,1500,2)		//Recoil script
    BREG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    BREG BC 3
    BREG D 3 A_FireCustomMissile("MeleeAttack2",4,0,0,-4)
    BREG E 3
    TNT1 AAA 3
    BREG FGH 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGE ABCDEF 3
    FRGE G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGE H 3
    FRGE H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGE I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGE I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGE I 0 A_TakeInventory("KnifeAttack",1)
    FRGE I 3 A_FireCustomMissile("MillsBomb",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor BrenEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor BrenShots : Inventory
{
    inventory.amount 0
    inventory.maxamount 2
}
actor BrenClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 30
    ammo.backpackamount 0
    ammo.backpackmaxamount 30
    +IGNORESKILL
}

actor BrenPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 1
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuffShotgun_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash2", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("bren/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("bren/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("bren/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("bren/fire4","SoundSlot6")
    stop
  }
}

actor BrenBullet : HHBulletPuff
{
  damagetype "Bren"
}
