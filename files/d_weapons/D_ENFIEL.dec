actor "Lee-Enfield" : RGUWeapon
{
  obituary "\cf%k \cc>> \csLee-Enfield \cc>> \cf%o"
  inventory.pickupmessage "Lee-Enfield"  
  weapon.selectionorder 19
  weapon.ammotype "EnfieldClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "EnfieldAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 10
  +NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",3)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    LEEA A 2 A_PlayWeaponSound("weapon/select")
    LEEA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    LEEA E 0 A_JumpIfNoAmmo("Reload")
    LEEA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    LEEA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    LEEA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    LEEA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEA E 1 A_WeaponReady
    loop 
  Aiming:
    LEEC E 0 A_JumpIfInventory("Reloading",1,"Reload")    
    LEEC E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    LEEC E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEC E 1 A_WeaponReady(WRF_NOBOB)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    LEEH ABCD 2
    TNT1 A 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    LEEA E 1 A_JumpIfInventory("Aiming",1,"FireAim")
    LEEA E 1 A_JumpIfInventory("EnfieldBolt",1,"Bolt")
    LEEB A 0 A_JumpIfNoAmmo("ReloadStart")
    LEEB A 0 A_TakeInventory("EnfieldClosed",1)
    LEEB A 0 A_FireCustomMissile("EnfieldPuff",0,0,4,2)
	LEEB A 1 BRIGHT A_FireBullets (10, 10, -1, 100, "EnfieldBullet3", 1, 3000)
    //LEEB A 1 BRIGHT A_FireCustomMissile("EnfieldBullet", random(0, 5), 1, -1, random(0, 5))
    LEEB B 0 ACS_ExecuteAlways(600,0,1500,150*random(2,4),0)		//Recoil script
    LEEB B 1 BRIGHT A_GunFlash
    LEEB CDEF 1 A_JumpIfNoAmmo("FireEmpty")
    LEEB G 5 A_WeaponReady(WRF_NOFIRE)
    LEEB G 0 A_GiveInventory("EnfieldBolt",1)
    LEEA E 5 A_WeaponReady(WRF_NOFIRE)
    goto InHand
    LEEB A 0 A_JumpIfNoAmmo(2)
    goto InHand
    LEEB A 0 A_GiveInventory("EnfieldEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    LEEB CDEF 1
    goto Fire+14
  FireAim:
    LEEC E 0 A_JumpIfInventory("EnfieldBolt",1,"Bolt")  
    LEEC E 1 A_JumpIfNoAmmo("ReloadStart")
    LEED A 0 A_TakeInventory("EnfieldClosed",1)
    LEED A 0 A_FireCustomMissile("EnfieldPuff",0,0,0,4)
    LEED A 1 BRIGHT A_FireBullets (0.35, 0.35, -1, 100, "EnfieldBullet3", 1, 9001)
    //LEED A 1 BRIGHT A_FireCustomMissile("EnfieldBullet", 0, 1, -1, 0)
    LEED B 0 ACS_ExecuteAlways(600,0,1500,150*random(1,2),0)			//Recoil script
    LEED B 1 BRIGHT A_GunFlash
    LEED B 1 A_ZoomFactor(1.18)
    LEED CDEF 1 A_JumpIfNoAmmo("FireEmptyAim")
    LEED F 1 A_ZoomFactor(1.2)
    LEED G 5 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    LEED G 0 A_GiveInventory("EnfieldBolt",1)
    LEED A 0 A_JumpIfInventory("EnfieldAmmo",1,2)
    LEED A 0 A_ReFire
    goto InHand
    LEED A 0 A_JumpIfNoAmmo(2)
    goto InHand
    LEED A 0 A_GiveInventory("EnfieldEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    LEED CDEF 1
    LEED F 0 A_TakeInventory("Aiming",1)
    goto Fire+19
  Hold:
    LEEA E 0 A_JumpIfInventory("Aiming",1,"HoldAim")	//Don't need a hold because of the already long rechamber time
    LEEA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    LEEA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    LEEC E 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("EnfieldAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    LEEE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    LEEE A 0 A_WeaponReady(WRF_NOFIRE)
    LEEE A 0 A_ZoomFactor(1.0)
    LEEE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE B 0 A_TakeInventory("EnfieldClosed",1)
    LEEE B 0 A_PlaySoundEx("kar98k/open","SoundSlot5")
    LEEE B 0 A_WeaponReady(WRF_NOFIRE)
    LEEE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    LEEE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE D 0 A_WeaponReady(WRF_NOFIRE)
    LEEE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE E 0 A_WeaponReady(WRF_NOFIRE)
    LEEE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE F 0 A_WeaponReady(WRF_NOFIRE)
    LEEE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE G 0 A_WeaponReady(WRF_NOFIRE)
    LEEE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE H 0 A_PlaySoundEx("kar98k/clipin","SoundSlot5")
    LEEE H 0 A_WeaponReady(WRF_NOFIRE)
    LEEE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE I 0 A_WeaponReady(WRF_NOFIRE)
    LEEE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE J 0 A_JumpIfInventory("EnfieldAmmo",6,1)
    Goto ReloadAmmo
    LEEE J 0 A_JumpIfInventory("EnfieldClip",5,"ReloadAmmo")	//Because the animation shows 2 clips, skip the second if 5 is already in
    LEEE J 0 A_WeaponReady(WRF_NOFIRE)
    LEEE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE K 0 A_WeaponReady(WRF_NOFIRE)
    LEEE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE L 0 A_PlaySoundEx("kar98k/clipin","SoundSlot5")
    LEEE L 0 A_WeaponReady(WRF_NOFIRE)
    LEEE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("EnfieldClip",1)
    TNT1 A 0 A_TakeInventory("EnfieldAmmo",1)
    TNT1 A 0 A_TakeInventory("EnfieldBolt",1)
    TNT1 A 0 A_JumpIfInventory("EnfieldClip",10,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("EnfieldAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    LEEE M 0 A_WeaponReady(WRF_NOFIRE)
    LEEE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE N 0 A_WeaponReady(WRF_NOFIRE)
    LEEE N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE O 0 A_GiveInventory("EnfieldClosed",1)
    LEEE O 0 A_PlaySoundEx("kar98k/close","SoundSlot5")
    LEEE O 0 A_WeaponReady(WRF_NOFIRE)
    LEEE O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LEEE P 0 A_WeaponReady(WRF_NOFIRE)
    LEEE P 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    LEEC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    LEEC A 0 A_ZoomFactor(1.2)		
    LEEC ABCDE 2
    LEEC E 0 A_GiveInventory("Aiming",1)
    LEEC E 0 ACS_ExecuteAlways(602,0,0,0,0)
    LEEC E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    LEEC E 0 A_ZoomFactor(1.0)
    LEEC EDCBA 2
    LEEC A 0 A_TakeInventory("Aiming",1)
    LEEC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    LEEC E 1 A_ZoomFactor(1.0)
    LEEC EDCBA 1
    LEEC A 0 A_TakeInventory("Aiming",1)
    LEEC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    LEEG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    LEEG B 3
    LEEG C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    LEEG DEF 3
    TNT1 A 3
    LEEG G 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGE ABCDEF 3
    FRGE G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGE H 3
    FRGE H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGE I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGE I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGE I 0 A_TakeInventory("KnifeAttack",1)
    FRGE I 3 A_FireCustomMissile("MillsBomb",0,0,0,16)
    goto Ready+1		//Reselect weapon
  Bolt:
    LEEB H 0 A_JumpIfInventory("Aiming",1,"BoltAim")
    LEEB H 0 A_GiveInventory("EnfieldClosed",1)
    LEEB H 0 A_TakeInventory("EnfieldBolt",1)
    LEEB H 5 A_PlayWeaponSound("kar98k/chamber")
    LEEB IJKL 5 A_WeaponReady(WRF_NOFIRE)
    goto InHand
  BoltAim:
    LEED H 0 A_ZoomFactor(1.0)
    LEED H 0 A_GiveInventory("EnfieldClosed",1)
    LEED H 0 A_TakeInventory("EnfieldBolt",1)
    LEED H 5 A_PlayWeaponSound("kar98k/chamber")
    LEED IJKL 5 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    LEED L 1 A_ZoomFactor(1.2)
    goto InHand
  }
}

actor EnfieldEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor EnfieldClosed : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor EnfieldClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 10
    ammo.backpackamount 0
    ammo.backpackmaxamount 10
    +IGNORESKILL
}

actor EnfieldPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuffShotgun_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash2", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("enfield/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("enfield/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("enfield/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("enfield/fire4","SoundSlot6")
    stop
  }
}

actor EnfieldBullet : HHBulletPuff
{
  damagetype "Enfield"
}
actor EnfieldBullet3 : HHBulletPuff
{
  damagetype "Enfield3"
}

actor EnfieldBolt : Inventory //If this is in the the players inventory the rifle needs to be bolted.
{
    inventory.amount 0
    inventory.maxamount 1
}
