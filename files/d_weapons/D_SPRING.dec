actor "Springfield" : RGUWeapon
{
  obituary "\cf%k \cc>> \cgSpringfield \cc>> \cf%o"
  inventory.pickupmessage "Springfield"  
  weapon.selectionorder 16
  weapon.ammotype "SpringfieldClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "SpringfieldAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 5
  +NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",4)	//Determines player speed - 4 means 30% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    FIEA A 2 A_PlayWeaponSound("weapon/select")
    FIEA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    FIEA E 0 A_JumpIfNoAmmo("Reload")
    FIEA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    FIEA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    FIEA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    FIEA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEA E 0 A_JumpIfInventory("SpringfieldClosed",1,1)
    FIEA E 1 A_WeaponReady
    loop 
  Aiming:
    SCOP A 0 A_JumpIfInventory("Reloading",1,"Reload")    
    SCOP A 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    SCOP A 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SCOP A 1 A_WeaponReady(WRF_NOBOB)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 4 means 30% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
    FIEH ABCD 2
    TNT1 A 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    FIEA E 0 A_JumpIfInventory("Reloading",1,"ReloadEnd")
    FIEA E 1 A_JumpIfInventory("Aiming",1,"FireAim")
    FIEA E 1 A_JumpIfInventory("SpringfieldBolt",1,"Bolt")
    FIEB A 0 A_JumpIfNoAmmo("ReloadStart")
    FIEB A 0 A_TakeInventory("SpringfieldClosed",1)
    FIEB A 0 A_FireCustomMissile("SpringfieldPuff",0,0,4,2)
    FIEB A 1 BRIGHT A_FireBullets (15, 15, -1, 100, "SpringfieldBullet2", 1, 3000)
    FIEB B 0 ACS_ExecuteAlways(600,0,1000,75*random(2,3),0)		//Recoil script
    FIEB B 1 BRIGHT A_GunFlash
    FIEB CDEF 1 A_JumpIfNoAmmo("FireEmpty")
    FIEB F 5 A_WeaponReady(WRF_NOFIRE)
	FIEB F 0 A_GiveInventory("SpringfieldBolt",1)
    FIEA E 5 A_WeaponReady(WRF_NOFIRE)
    goto InHand
    FIEB A 0 A_JumpIfNoAmmo(2)
    goto InHand
    FIEB A 0 A_GiveInventory("SpringfieldEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    FIEB CDEF 1
    goto Fire+17
  FireAim:
    SCOP A 1 A_JumpIfInventory("SpringfieldBolt",1,"BoltAim")
    SCOP A 1 A_JumpIfNoAmmo("ReloadStart")
    SCOP A 0 A_TakeInventory("SpringfieldClosed",1)
    SCOP A 0 A_FireCustomMissile("SpringfieldPuff",0,0,0,4)
    SCOP A 1 BRIGHT A_FireBullets (0.1, 0.1, 2, 100, "SpringfieldBullet", 1, 9001) //45, 90, 135, 180, 225, 270
    SCOP A 0 ACS_ExecuteAlways(600,0,1000,75*random(1,2),0)		//Recoil script
    SCOP A 1 BRIGHT A_GunFlash
    SCOP A 4 A_ZoomFactor(1.5)
    SCOP AAAA 1 A_JumpIfNoAmmo("FireEmptyAim")
    SCOP A 5 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB|WRF_NOFIRE)
    SCOP A 4 A_ZoomFactor(3.5)
	SCPP A 0 A_GiveInventory("SpringfieldBolt",1)
    SCOP A 0 A_JumpIfInventory("SpringfieldAmmo",1,2)
    SCOP A 0 A_ReFire
    goto InHand
    SCOP A 0 A_JumpIfNoAmmo(2)
    goto InHand
    SCOP A 0 A_GiveInventory("SpringfieldEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    SCOP AAAA 1
    goto Fire+17
  FireAimStill:
    SCOP A 1 BRIGHT A_FireBullets (0.1, 0.1, 2, 100, "SpringfieldBullet", 1, 9001) //45, 90, 135, 180, 225, 270
    goto FireAim+5
  Hold:
    K98A E 0 A_JumpIfInventory("Aiming",1,"HoldAim")	//Don't need a hold because of the already long rechamber time
    K98A E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    K98A E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    K98C E 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("SpringfieldAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    FIEE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    FIEE N 0 A_GiveInventory("Reloading",1)
    FIEE A 0 A_WeaponReady(WRF_NOFIRE)
    FIEE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE B 0 A_TakeInventory("SpringfieldClosed",1)
    FIEE B 0 A_PlaySoundEx("kar98k/open","SoundSlot5")
    FIEE B 0 A_WeaponReady(WRF_NOFIRE)
    FIEE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    FIEE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE D 0 A_WeaponReady(WRF_NOFIRE)
    FIEE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE E 0 A_WeaponReady(WRF_NOFIRE)
    FIEE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE F 0 A_WeaponReady(WRF_NOFIRE)
    FIEE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE G 0 A_WeaponReady(WRF_NOFIRE)
    FIEE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE H 0 A_WeaponReady(WRF_NOFIRE)
    FIEE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE I 0 A_WeaponReady(WRF_NOFIRE)
    FIEE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("SpringfieldClip",1)
    TNT1 A 0 A_TakeInventory("SpringfieldAmmo",1)
    TNT1 A 0 A_TakeInventory("SpringfieldBolt",1)
    FIEE G 0 A_PlaySoundEx("spring/shell","SoundSlot5")
    FIEE G 0 A_WeaponReady(WRF_NOBOB)
    FIEE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE H 0 A_WeaponReady(WRF_NOBOB)
    FIEE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE I 0 A_WeaponReady(WRF_NOBOB)
    FIEE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE J 0 A_WeaponReady(WRF_NOBOB)
    FIEE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE K 0 A_WeaponReady(WRF_NOBOB)
    FIEE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE L 0 A_WeaponReady(WRF_NOBOB)
    FIEE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_JumpIfInventory("SpringfieldClip",5,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("SpringfieldAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    FIEE M 0 A_WeaponReady(WRF_NOFIRE)
    FIEE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE N 0 A_GiveInventory("SpringfieldClosed",1)
    FIEE N 0 A_PlaySoundEx("kar98k/close","SoundSlot5")
    FIEE N 0 A_WeaponReady(WRF_NOFIRE)
    FIEE N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE O 0 A_WeaponReady(WRF_NOFIRE)
    FIEE O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    FIEE P 0 A_WeaponReady(WRF_NOFIRE)
    FIEE P 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto InHand
  AltFire:	//Aim
    FIEA E 0 A_JumpIfInventory("Reloading",1,"ReloadEnd")
    FIEC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    FIEC ABC 2
    FIEC A 0 A_ZoomFactor(3.0,ZOOM_INSTANT)
    FIEC E 0 A_GiveInventory("Aiming",1)
    FIEC E 0 ACS_ExecuteAlways(602,0,1,0,0)
    FIEC E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    FIEC C 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
    FIEC CBA 2
    FIEC A 0 A_TakeInventory("Aiming",1)
    FIEC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    FIEC C 1 A_ZoomFactor(1.0,ZOOM_INSTANT)
    FIEC CBA 1
    FIEC A 0 A_TakeInventory("Aiming",1)
    FIEC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    FIEG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    FIEG B 3
    FIEG C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    FIEG D 3
    TNT1 AA 3
    FIEG EF 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGA ABCDEF 3
    FRGA G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGA H 3
    FRGA H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGA I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGA I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGA I 0 A_TakeInventory("KnifeAttack",1)
    FRGA I 3 A_FireCustomMissile("FragGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  Bolt:
    FIEB H 0 A_JumpIfInventory("Aiming",1,"BoltAim")
    FIEB H 0 A_GiveInventory("SpringfieldClosed",1)
    FIEB H 0 A_TakeInventory("SpringfieldBolt",1)
    FIEB H 5 A_PlayWeaponSound("kar98k/chamber")
    FIEB IJKL 5 A_WeaponReady(WRF_NOFIRE)
    goto InHand
  BoltAim:
    FIEC C 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
    FIEC CBA 2
    FIEC A 0 A_TakeInventory("Aiming",1)
    FIEC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    FIEB H 0 A_GiveInventory("SpringfieldClosed",1)
    FIEB H 0 A_TakeInventory("SpringfieldBolt",1)
    FIEB H 5 A_PlayWeaponSound("kar98k/chamber")
    FIEB IJKL 5 A_WeaponReady(WRF_NOFIRE)
    goto InHand
  }
}

actor SpringfieldEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor SpringfieldClosed : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor SpringfieldClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 5
    ammo.backpackamount 0
    ammo.backpackmaxamount 5
    +IGNORESKILL
}

actor SpringfieldPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuffShotgun_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash2", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("Spring/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("Spring/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("Spring/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("Spring/fire4","SoundSlot6")
    stop
  }
}

actor SpringfieldBullet : HHBulletPuff
{
  damagetype "Springfield"
}

actor SpringfieldBullet2 : HHBulletPuff
{
  damagetype "Springfield2"
}

actor SpringfieldBolt : Inventory //If this is in the players inventory the weapon needs to be bolted.
{
	inventory.amount 0
	inventory.maxamount 1
}
