actor "MP44" : RGUWeapon
{
  obituary "\cf%k \cc>> \cqMP44 \cc>> \cf%o"
  inventory.pickupmessage "MP44"  
  weapon.selectionorder 15
  weapon.ammotype "MP44Clip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "MP44Ammo"
  weapon.ammouse2 0
  weapon.ammogive2 30
  -NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",4)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    M44A A 2 A_PlayWeaponSound("weapon/select")
    M44A BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    M44A E 0 A_JumpIfNoAmmo("Reload")
    M44A E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    M44A E 0 A_JumpIfInventory("Reloading",1,"Reload")
    M44A E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M44A E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44A E 1 A_WeaponReady
    loop 
  Aiming:
    M44C E 0 A_JumpIfInventory("Reloading",1,"Reload")    
    M44C E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M44C E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44C E 1 A_WeaponReady(WRF_NOBOB)
    M44C E 0 A_ZoomFactor(1.2)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    M44H ABC 2
    TNT1 AA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    M44A E 0 A_JumpIfInventory("Aiming",1,"FireAim")
    M44B A 0 A_JumpIfNoAmmo("ReloadStart")
    M44B A 0 A_FireCustomMissile("MP44Puff",0,0,6,-2)
    M44B A 1 BRIGHT A_FireBullets (25, 25, -1, 75, "MP44Bullet", 1, 3000)
    M44B B 0 ACS_ExecuteAlways(600,0,900,170*random(2,4),0)		//Recoil script - 600 recoil, don't revert aim
    M44B B 1 BRIGHT A_GunFlash
    M44B CD 1 A_JumpIfNoAmmo("FireEmpty")
    M44B A 0 A_JumpIfInventory("MP44Ammo",1,2)
    M44B A 0 A_ReFire
    goto InHand
    M44B A 0 A_JumpIfNoAmmo(2)
    M44B A 0 A_ReFire
    goto InHand
    M44B A 0 A_GiveInventory("MP44Empty",1)		//Go to reload
    goto Reload
  FireEmpty:
    M44B ABC 1 
    M44B D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+8
  FireAim:
    M44C E 0 A_JumpIfNoAmmo("ReloadStart")
    M44D A 0 A_FireCustomMissile("MP44Puff",0,0,0,6)
    M44D A 1 BRIGHT A_FireBullets (0.35, 0.35, -1, 75, "MP44Bullet", 1, 5000)
    M44D B 0 ACS_ExecuteAlways(600,0,900,170*random(1,2),0)			//Recoil script - 600 recoil, revert aim
    M44D B 1 BRIGHT A_GunFlash
    M44D B 0 A_ZoomFactor(1.18)
    M44D CD 1 A_JumpIfNoAmmo("FireEmptyAim")
    M44D A 0 A_JumpIfInventory("MP44Ammo",1,2)
    M44D A 0 A_ReFire
    goto Aiming
    M44D A 0 A_JumpIfNoAmmo(2)
    M44D A 0 A_ReFire
    goto Aiming
    M44D A 0 A_GiveInventory("MP44Empty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    M44D ABC 1 
    M44D D 1 A_PlayWeaponSound("weapon/dryfire")
    goto FireAim+8
  Hold:
    M44A E 0 A_JumpIfInventory("Aiming",1,"HoldAim")
    M44A E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M44A E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    M44C D 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("MP44Ammo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    M44E A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    M44E A 0 A_WeaponReady(WRF_NOFIRE)
    M44E A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44E B 0 A_WeaponReady(WRF_NOFIRE)
    M44E B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44E C 0 A_PlaySoundEx("MP44/clipout","SoundSlot5")
    M44E C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    M44E C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44E D 0 A_WeaponReady(WRF_NOFIRE)
    M44E D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44E E 0 A_WeaponReady(WRF_NOFIRE)
    M44E E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44E F 0 A_WeaponReady(WRF_NOFIRE)
    M44E F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44E G 0 A_WeaponReady(WRF_NOFIRE)
    M44E G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44E H 0 A_PlaySoundEx("MP44/clipin","SoundSlot5")
    M44E H 0 A_WeaponReady(WRF_NOFIRE)
    M44E H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44E I 0 A_WeaponReady(WRF_NOFIRE)
    M44E I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44E J 0 A_WeaponReady(WRF_NOFIRE)
    M44E J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44E K 0 A_WeaponReady(WRF_NOFIRE)
    M44E K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("MP44Clip",1)
    TNT1 A 0 A_TakeInventory("MP44Ammo",1)
    TNT1 A 0 A_JumpIfInventory("MP44Clip",30,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("MP44Ammo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_JumpIfInventory("MP44Empty",1,"ReloadEndEmpty")
    M44E L 0 A_WeaponReady(WRF_NOFIRE)
    M44E L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  ReloadEndEmpty:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_TakeInventory("MP44Empty",1)
    M44F A 0 A_WeaponReady(WRF_NOFIRE)
    M44F A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44F B 0 A_WeaponReady(WRF_NOFIRE)
    M44F B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44F C 0 A_PlaySoundEx("MP44/bolt","SoundSlot5")
    M44F C 0 A_WeaponReady(WRF_NOFIRE)
    M44F C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44F D 0 A_WeaponReady(WRF_NOFIRE)
    M44F D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44F E 0 A_WeaponReady(WRF_NOFIRE)
    M44F E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M44F F 0 A_WeaponReady(WRF_NOFIRE)
    M44F F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    M44C A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    M44C A 0 A_ZoomFactor(1.2)		
    M44C ABCDE 2
    M44C E 0 A_GiveInventory("Aiming",1)
    M44C E 0 ACS_ExecuteAlways(602,0,0,0,0)
    M44C E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    M44C E 0 A_ZoomFactor(1.0)
    M44C EDCBA 2
    M44C A 0 A_TakeInventory("Aiming",1)
    M44C A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    M44C E 1 A_ZoomFactor(1.0)
    M44C EDCBA 1
    M44C A 0 A_TakeInventory("Aiming",1)
    M44C A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    M44G A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    M44G B 3
    M44G C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    M44G DEFGH 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGC ABCDEFG 3
    TNT1 A 15 
    FRGC H 3
    FRGC H 0 A_PlaySoundEx("stick/throw","SoundSlot5")
    FRGC I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGC I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGC I 0 A_TakeInventory("KnifeAttack",1)
    FRGC I 3 A_FireCustomMissile("StickGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor MP44Empty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor MP44Clip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 30
    ammo.backpackamount 0
    ammo.backpackmaxamount 30
    +IGNORESKILL
}

actor MP44Puff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuffShotgun_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash2", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("MP44/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("MP44/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("MP44/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("MP44/fire4","SoundSlot6")
    stop
  }
}

actor MP44Bullet : HHBulletPuff
{
  damagetype "MP44"
}
