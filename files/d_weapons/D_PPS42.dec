actor "PPS-42" : RGUWeapon
{
  obituary "\cf%k \cc>> \cdPPS-42 \cc>> \cf%o"
  inventory.pickupmessage "PPS-42"  
  weapon.selectionorder 20
  weapon.ammotype "PPS42Clip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "PPS42Ammo"
  weapon.ammouse2 0
  weapon.ammogive2 35
  -NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",2)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    PPSA A 2 A_PlayWeaponSound("weapon/select")
    PPSA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    PPSA E 0 A_JumpIfNoAmmo("Reload")
    PPSA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    PPSA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    PPSA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    PPSA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSA E 1 A_WeaponReady
    loop 
  Aiming:
    PPSC D 0 A_JumpIfInventory("Reloading",1,"Reload")    
    PPSC D 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    PPSC D 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSC D 1 A_WeaponReady(WRF_NOBOB)
    PPSC D 0 A_ZoomFactor(1.2)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    PPSH ABC 2
    TNT1 AA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    PPSA E 0 A_JumpIfInventory("Aiming",1,"FireAim")
    PPSB A 0 A_JumpIfNoAmmo("ReloadStart")
    PPSB A 0 A_FireCustomMissile("PPS42Puff",0,0,5,0)
    PPSB A 1 BRIGHT A_FireBullets (8, 8, -1, 45, "PPS42Bullet", 1, 3000)
    PPSB B 0 ACS_ExecuteAlways(600,0,500,100*random(2,4),0)	//Recoil script
    PPSB B 1 BRIGHT A_GunFlash
    PPSB CD 1 A_JumpIfNoAmmo("FireEmpty")
    PPSB A 0 A_JumpIfInventory("PPS42Ammo",1,2)
    PPSB A 0 A_ReFire
    goto InHand
    PPSB A 0 A_JumpIfNoAmmo(2)
    PPSB A 0 A_ReFire
    goto InHand
    PPSB A 0 A_GiveInventory("PPS42Empty",1)		//Go to reload
    goto Reload
  FireEmpty:
    PPSB ABC 1 
    PPSB D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+8
  FireAim:
    PPSC D 0 A_JumpIfNoAmmo("ReloadStart")
    PPSD A 0 A_FireCustomMissile("PPS42Puff",0,0,0,5)
    PPSD A 1 BRIGHT A_FireBullets (0.9, 0.9, -1, 45, "PPS42Bullet", 1, 5000)
    PPSD B 0 ACS_ExecuteAlways(600,0,500,100*random(1,2),0)	//Recoil script
    PPSD B 1 BRIGHT A_GunFlash
    PPSD B 0 A_ZoomFactor(1.18)
    PPSD CD 1 A_JumpIfNoAmmo("FireEmptyAim")
    PPSD A 0 A_JumpIfInventory("PPS42Ammo",1,2)
    PPSD A 0 A_ReFire
    goto Aiming
    PPSD A 0 A_JumpIfNoAmmo(2)
    PPSD A 0 A_ReFire
    goto Aiming
    PPSD A 0 A_GiveInventory("PPS42Empty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    PPSD ABC 1 
    PPSD D 1 A_PlayWeaponSound("weapon/dryfire")
    goto FireAim+8
  Hold:
    PPSA E 0 A_JumpIfInventory("Aiming",1,"HoldAim")
    PPSA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    PPSA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    PPSC D 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("PPS42Ammo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    PPSE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    PPSE A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE B 0 A_PlaySoundEx("pps2/clipout","SoundSlot5")
    PPSE B 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE C 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)         //This apparent "messy" setup allows for interruption at all times
    PPSE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE D 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE E 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE F 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE G 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE H 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE I 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE J 0 A_PlaySoundEx("pps2/clipin","SoundSlot5")
    PPSE J 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE K 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("PPS42Clip",1)
    TNT1 A 0 A_TakeInventory("PPS42Ammo",1)
    TNT1 A 0 A_JumpIfInventory("PPS42Clip",35,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("PPS42Ammo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    PPSE L 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE M 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_JumpIfInventory("PPS42Empty",1,"ReloadEndEmpty")
    PPSE N 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSE O 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSE O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  ReloadEndEmpty:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_TakeInventory("PPS42Empty",1)
    PPSF A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSF A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSF B 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSF B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSF C 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSF C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSF D 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSF D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSF E 0 A_PlaySoundEx("pps2/bolt","SoundSlot5")
    PPSF E 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSF E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSF F 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSF F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPSF G 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPSF G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    PPSC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    PPSC A 0 A_ZoomFactor(1.2)		
    PPSC ABCD 2
    PPSC D 0 A_GiveInventory("Aiming",1)
    PPSC D 0 ACS_ExecuteAlways(602,0,0,0,0)
    PPSC D 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    PPSC D 0 A_ZoomFactor(1.0)
    PPSC DCBA 2
    PPSC A 0 A_TakeInventory("Aiming",1)
    PPSC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    PPSC D 1 A_ZoomFactor(1.0)
    PPSC DCBA 1
    PPSC A 0 A_TakeInventory("Aiming",1)
    PPSC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    PPSG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    PPSG B 3
    PPSG C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    PPSG DE 3
    TNT1 A 3
    PPSG FG 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGD ABCDEFG 3
    TNT1 A 15 
    FRGD H 3
    FRGD H 0 A_PlaySoundEx("stick/throw","SoundSlot5")
    FRGD I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGD I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGD I 0 A_TakeInventory("KnifeAttack",1)
    FRGD I 3 A_FireCustomMissile("RussianGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor PPS42Empty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor PPS42Clip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 35
    ammo.backpackamount 0
    ammo.backpackmaxamount 35
    +IGNORESKILL
}

actor PPS42Puff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 1
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("ppsh/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("ppsh/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("ppsh/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("ppsh/fire4","SoundSlot6")
    stop
  }
}

actor PPS42Bullet : HHBulletPuff
{
  damagetype "PPS42"
}
