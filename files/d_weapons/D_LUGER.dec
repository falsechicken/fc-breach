actor "Luger" : RGUWeapon
{
  obituary "\cf%k \cc>> \ciLuger \cc>> \cf%o"
  inventory.pickupmessage "Luger"  
  weapon.selectionorder 21
  weapon.ammotype "LugerClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "LugerAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 8
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",1)	//Determines player speed - 1 means normal speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    LUGA A 2 A_PlayWeaponSound("weapon/select")
    LUGA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    LUGA E 0 A_JumpIfNoAmmo("Reload")
    LUGA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    LUGA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    LUGA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    LUGA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGA E 1 A_WeaponReady
    loop 
  Aiming:
    LUGC D 0 A_JumpIfInventory("Reloading",1,"Reload")    
    LUGC D 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    LUGC D 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGC D 1 A_WeaponReady(WRF_NOBOB)
    LUGC D 0 A_ZoomFactor(1.0)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 1 means normal speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    LUGA EDCBA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    LUGA E 0 A_JumpIfInventory("Aiming",1,"FireAim")
    LUGA E 1 A_JumpIfNoAmmo("ReloadStart")
    LUGB A 0 A_FireCustomMissile("LugerPuff",0,0,16,1)
    LUGB A 1 BRIGHT A_FireBullets (6, 6, -1, 50, "LugerBullet", 1, 2300)
    LUGB B 0 ACS_ExecuteAlways(600,0,600,50*random(2,4),0)	//Recoil script
    LUGB B 1 BRIGHT A_GunFlash
    LUGB B 0 A_JumpIfNoAmmo("FireEmpty")
    LUGB CDEFGH 1 A_WeaponReady
    LUGB A 0 A_JumpIfInventory("LugerAmmo",1,2)
    LUGB A 0 A_ReFire
    goto InHand
    LUGB A 0 A_JumpIfNoAmmo(2)
    LUGB A 0 A_ReFire
    goto InHand
    LUGB A 0 A_GiveInventory("LugerEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    LUGB CDEFG 1 
    LUGB H 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+13
  FireAim:
    LUGC D 1 A_JumpIfNoAmmo("ReloadStart")
    LUGD A 0 A_FireCustomMissile("LugerPuff",0,0,0,6)
    LUGD A 1 BRIGHT A_FireBullets (0.5, 0.5, -1, 50, "LugerBullet", 1, 2300)
    LUGD B 0 ACS_ExecuteAlways(600,0,600,50*random(1,2),0)		//Recoil script
    LUGD B 1 BRIGHT A_GunFlash
    LUGD B 0 A_ZoomFactor(0.98)
    LUGD B 0 A_JumpIfNoAmmo("FireEmptyAim")
    LUGD CDEFGH 1 A_WeaponReady
    LUGD A 0 A_JumpIfInventory("LugerAmmo",1,2)
    LUGD A 0 A_ReFire
    goto Aiming
    LUGD A 0 A_JumpIfNoAmmo(2)
    LUGD A 0 A_ReFire
    goto Aiming
    LUGD A 0 A_GiveInventory("LugerEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    LUGD CDEFG 1 
    LUGD H 1 A_PlayWeaponSound("weapon/dryfire")
    goto FireAim+12
  Hold:
    LUGA E 3 A_JumpIfInventory("Aiming",1,"HoldAim")
    LUGA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    LUGA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGA E 0 A_JumpIfInventory("IsBot",1,"Fire")
    LUGA E 1 A_ReFire
    goto InHand
  HoldAim:
    LUGC D 3
    LUGC D 0 A_JumpIfInventory("IsBot",1,"FireAim")
    LUGC D 1 A_ReFire
    goto Aiming
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("LugerAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    LUGE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    LUGE A 0 A_JumpIfInventory("LugerEmpty",1,"ReloadStartEmpty")
    LUGE A 0 A_PlaySoundEx("luger/clipout","SoundSlot5")
    LUGE A 0 A_WeaponReady(WRF_NOFIRE)
    LUGE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGE B 0 A_WeaponReady(WRF_NOFIRE)
    LUGE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGE C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    LUGE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGE D 0 A_WeaponReady(WRF_NOFIRE)
    LUGE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGE E 0 A_WeaponReady(WRF_NOFIRE)
    LUGE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGE F 0 A_WeaponReady(WRF_NOFIRE)
    LUGE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGE G 0 A_PlaySoundEx("luger/clipin","SoundSlot5")
    LUGE G 0 A_WeaponReady(WRF_NOFIRE)
    LUGE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGE H 0 A_WeaponReady(WRF_NOFIRE)
    LUGE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGE I 0 A_WeaponReady(WRF_NOFIRE)
    LUGE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGE J 0 A_WeaponReady(WRF_NOFIRE)
    LUGE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGE K 0 A_WeaponReady(WRF_NOFIRE)
    LUGE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("LugerClip",1)
    TNT1 A 0 A_TakeInventory("LugerAmmo",1)
    TNT1 A 0 A_JumpIfInventory("LugerClip",8,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("LugerAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    LUGE L 0 A_WeaponReady(WRF_NOFIRE)
    LUGE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGE M 0 A_WeaponReady(WRF_NOFIRE)
    LUGE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  ReloadStartEmpty:
    LUGF A 0 A_WeaponReady(WRF_NOFIRE)
    LUGF A 0 A_PlaySoundEx("luger/clipout","SoundSlot5")
    LUGF A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF B 0 A_WeaponReady(WRF_NOFIRE)
    LUGF B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    LUGF C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF D 0 A_WeaponReady(WRF_NOFIRE)
    LUGF D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF E 0 A_WeaponReady(WRF_NOFIRE)
    LUGF E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF F 0 A_WeaponReady(WRF_NOFIRE)
    LUGF F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF G 0 A_PlaySoundEx("luger/clipin","SoundSlot5")
    LUGF G 0 A_WeaponReady(WRF_NOFIRE)
    LUGF G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF H 0 A_WeaponReady(WRF_NOFIRE)
    LUGF H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF I 0 A_WeaponReady(WRF_NOFIRE)
    LUGF I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF J 0 A_WeaponReady(WRF_NOFIRE)
    LUGF J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF K 0 A_WeaponReady(WRF_NOFIRE)
    LUGF K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF L 0 A_PlaySoundEx("luger/bolt","SoundSlot5")
    LUGF L 0 A_WeaponReady(WRF_NOFIRE)
    LUGF L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmoEmpty
  ReloadAmmoEmpty:
    TNT1 A 0 A_GiveInventory("LugerClip",1)
    TNT1 A 0 A_TakeInventory("LugerAmmo",1)
    TNT1 A 0 A_JumpIfInventory("LugerClip",8,"ReloadEndEmpty")
    TNT1 A 0 A_JumpIfInventory("LugerAmmo",1,"ReloadAmmoEmpty")
    goto ReloadEndEmpty
  ReloadEndEmpty:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_TakeInventory("LugerEmpty",1)
    LUGF M 0 A_WeaponReady(WRF_NOFIRE)
    LUGF M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF N 0 A_WeaponReady(WRF_NOFIRE)
    LUGF N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    LUGF O 0 A_WeaponReady(WRF_NOFIRE)
    LUGF O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    LUGC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    LUGC A 0 A_ZoomFactor(1.0)		//No FOV change for handguns
    LUGC ABCD 2
    LUGC D 0 A_GiveInventory("Aiming",1)
    LUGC D 0 ACS_ExecuteAlways(602,0,0,0,0)
    LUGC D 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    LUGC D 0 A_ZoomFactor(1.0)
    LUGC DCBA 2
    LUGC A 0 A_TakeInventory("Aiming",1)
    LUGC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    LUGC D 1 A_ZoomFactor(1.0)
    LUGC DCBA 1
    LUGC A 0 A_TakeInventory("Aiming",1)
    LUGC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,1)		//Recoil script
    LUGG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    LUGG BC 3
    LUGG D 3 A_FireCustomMissile("MeleeAttack",0,0,0,0)
    TNT1 AA 3
    LUGG EF 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGC ABCDEFG 3
    TNT1 A 15 
    FRGC H 3
    FRGC H 0 A_PlaySoundEx("stick/throw","SoundSlot5")
    FRGC I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGC I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGC I 0 A_TakeInventory("KnifeAttack",1)
    FRGC I 3 A_FireCustomMissile("StickGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor LugerEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor LugerClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 8
    ammo.backpackamount 0
    ammo.backpackmaxamount 8
    +IGNORESKILL
}

actor LugerPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("luger/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("luger/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("luger/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("luger/fire4","SoundSlot6")
    stop
  }
}


actor LugerBullet : HHBulletPuff
{
  damagetype "Luger"
}
