actor "TT30" : RGUWeapon
{
  obituary "\cf%k \cc>> \ciTT30 \cc>> \cf%o"
  inventory.pickupmessage "TT30"  
  weapon.selectionorder 21
  weapon.ammotype "TT30Clip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "TT30Ammo"
  weapon.ammouse2 0
  weapon.ammogive2 8
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",1)	//Determines player speed - 1 means normal speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TT3A A 2 A_PlayWeaponSound("weapon/select")
    TT3A BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    TT3A E 0 A_JumpIfNoAmmo("Reload")
    TT3A E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    TT3A E 0 A_JumpIfInventory("Reloading",1,"Reload")
    TT3A E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    TT3A E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3A E 1 A_WeaponReady
    loop 
  Aiming:
    TT3C D 0 A_JumpIfInventory("Reloading",1,"Reload")    
    TT3C D 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    TT3C D 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3C D 1 A_WeaponReady(WRF_NOBOB)
    TT3C D 0 A_ZoomFactor(1.0)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 1 means normal speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    TT3A EDCBA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    TT3A E 0 A_JumpIfInventory("Aiming",1,"FireAim")
    TT3A E 1 A_JumpIfNoAmmo("ReloadStart")
    TT3B A 0 A_FireCustomMissile("Colt45Puff",0,0,12,1)
    TT3B A 1 BRIGHT A_FireBullets (6, 6, -1, 45, "TT30Bullet", 1, 2300)
    TT3B B 0 ACS_ExecuteAlways(600,0,600,50*random(2,4),0)	//Recoil script
    TT3B B 1 BRIGHT A_GunFlash
    TT3B B 0 A_JumpIfNoAmmo("FireEmpty")
    TT3B CDEFGH 1 A_WeaponReady
    TT3B A 0 A_JumpIfInventory("TT30Ammo",1,2)
    TT3B A 0 A_ReFire
    goto InHand
    TT3B A 0 A_JumpIfNoAmmo(2)
    TT3B A 0 A_ReFire
    goto InHand
    TT3B A 0 A_GiveInventory("TT30Empty",1)		//Go to reload
    goto Reload
  FireEmpty:
    TT3B CDEFG 1 
    TT3B H 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+13
  FireAim:
    TT3C D 1 A_JumpIfNoAmmo("ReloadStart")
    TT3D A 0 A_FireCustomMissile("Colt45Puff",0,0,0,6)
    TT3D A 1 BRIGHT A_FireBullets (0.5, 0.5, -1, 45, "TT30Bullet", 1, 2300)
    TT3D B 0 ACS_ExecuteAlways(600,0,600,50*random(1,2),0) //Recoil script
    TT3D B 1 BRIGHT A_GunFlash
    TT3D B 1 A_ZoomFactor(0.98)
    TT3D B 0 A_JumpIfNoAmmo("FireEmptyAim")
    TT3D CDEFGH 1 A_WeaponReady
    TT3D A 0 A_JumpIfInventory("TT30Ammo",1,2)
    TT3D A 0 A_ReFire
    goto Aiming
    TT3D A 0 A_JumpIfNoAmmo(2)
    TT3D A 0 A_ReFire
    goto Aiming
    TT3D A 0 A_GiveInventory("TT30Empty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    TT3D CDEFG 1 
    TT3D H 1 A_PlayWeaponSound("weapon/dryfire")
    goto FireAim+13
  Hold:
    TT3A E 3 A_JumpIfInventory("Aiming",1,"HoldAim")
    TT3A E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    TT3A E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3A E 0 A_JumpIfInventory("IsBot",1,"Fire")
    TT3A E 1 A_ReFire
    goto InHand
  HoldAim:
    TT3C D 3
    TT3C D 0 A_JumpIfInventory("IsBot",1,"FireAim")
    TT3C D 1 A_ReFire
    goto Aiming
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("TT30Ammo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    TT3E A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    TT3E A 0 A_JumpIfInventory("TT30Empty",1,"ReloadStartEmpty")
    TT3E A 0 A_WeaponReady(WRF_NOFIRE)
    TT3E A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E B 0 A_PlaySoundEx("colt45/clipout","SoundSlot5")
    TT3E B 0 A_WeaponReady(WRF_NOFIRE)
    TT3E B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    TT3E C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E D 0 A_WeaponReady(WRF_NOFIRE)
    TT3E D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E E 0 A_WeaponReady(WRF_NOFIRE)
    TT3E E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E F 0 A_WeaponReady(WRF_NOFIRE)
    TT3E F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E G 0 A_WeaponReady(WRF_NOFIRE)
    TT3E G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E H 0 A_PlaySoundEx("colt45/clipin","SoundSlot5")
    TT3E H 0 A_WeaponReady(WRF_NOFIRE)
    TT3E H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E I 0 A_WeaponReady(WRF_NOFIRE)
    TT3E I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E J 0 A_WeaponReady(WRF_NOFIRE)
    TT3E J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E K 0 A_WeaponReady(WRF_NOFIRE)
    TT3E K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("TT30Clip",1)
    TNT1 A 0 A_TakeInventory("TT30Ammo",1)
    TNT1 A 0 A_JumpIfInventory("TT30Clip",8,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("TT30Ammo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TT3E L 0 A_WeaponReady(WRF_NOFIRE)
    TT3E L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E M 0 A_WeaponReady(WRF_NOFIRE)
    TT3E M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3E N 0 A_WeaponReady(WRF_NOFIRE)
    TT3E N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  ReloadStartEmpty:
    TT3F A 0 A_WeaponReady(WRF_NOFIRE)
    TT3F A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F B 0 A_PlaySoundEx("colt45/clipout","SoundSlot5")
    TT3F B 0 A_WeaponReady(WRF_NOFIRE)
    TT3F B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    TT3F C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F D 0 A_WeaponReady(WRF_NOFIRE)
    TT3F D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F E 0 A_WeaponReady(WRF_NOFIRE)
    TT3F E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F F 0 A_WeaponReady(WRF_NOFIRE)
    TT3F F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F G 0 A_WeaponReady(WRF_NOFIRE)
    TT3F G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F H 0 A_PlaySoundEx("colt45/clipin","SoundSlot5")
    TT3F H 0 A_WeaponReady(WRF_NOFIRE)
    TT3F H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F I 0 A_WeaponReady(WRF_NOFIRE)
    TT3F I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F J 0 A_WeaponReady(WRF_NOFIRE)
    TT3F J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F K 0 A_WeaponReady(WRF_NOFIRE)
    TT3F K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F L 0 A_WeaponReady(WRF_NOFIRE)
    TT3F L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmoEmpty
  ReloadAmmoEmpty:
    TNT1 A 0 A_GiveInventory("TT30Clip",1)
    TNT1 A 0 A_TakeInventory("TT30Ammo",1)
    TNT1 A 0 A_JumpIfInventory("TT30Clip",8,"ReloadEndEmpty")
    TNT1 A 0 A_JumpIfInventory("TT30Ammo",1,"ReloadAmmoEmpty")
    goto ReloadEndEmpty
  ReloadEndEmpty:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_TakeInventory("TT30Empty",1)
    TT3F M 0 A_PlaySoundEx("colt45/slide","SoundSlot5")
    TT3F M 0 A_WeaponReady(WRF_NOFIRE)
    TT3F M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TT3F N 0 A_WeaponReady(WRF_NOFIRE)
    TT3F N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    TT3C A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    TT3C A 0 A_ZoomFactor(1.0)		//No FOV change for handguns
    TT3C ABCD 2
    TT3C D 0 A_GiveInventory("Aiming",1)
    TT3C D 0 ACS_ExecuteAlways(602,0,0,0,0)
    TT3C D 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    TT3C D 0 A_ZoomFactor(1.0)
    TT3C DCBA 2
    TT3C A 0 A_TakeInventory("Aiming",1)
    TT3C A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    TT3C D 1 A_ZoomFactor(1.0)
    TT3C DCBA 1
    TT3C A 0 A_TakeInventory("Aiming",1)
    TT3C A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,1)		//Recoil script
    TT3G A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    TT3G BC 3
    TT3G D 3 A_FireCustomMissile("MeleeAttack",0,0,0,0)
    TNT1 AA 3
    TT3G EF 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGD ABCDEFG 3
    TNT1 A 15 
    FRGD H 3
    FRGD H 0 A_PlaySoundEx("stick/throw","SoundSlot5")
    FRGD I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGD I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGD I 0 A_TakeInventory("KnifeAttack",1)
    FRGD I 3 A_FireCustomMissile("RussianGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor TT30Empty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor TT30Clip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 8
    ammo.backpackamount 0
    ammo.backpackmaxamount 8
    +IGNORESKILL
}

actor TT30Bullet : HHBulletPuff
{
  damagetype "TT30"
}
