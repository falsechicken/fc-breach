actor "MP40" : RGUWeapon
{
  obituary "\cf%k \cc>> \cdMP40 \cc>> \cf%o"
  inventory.pickupmessage "MP40"  
  weapon.selectionorder 20
  weapon.ammotype "MP40Clip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "MP40Ammo"
  weapon.ammouse2 0
  weapon.ammogive2 32
  -NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",2)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    M40A A 2 A_PlayWeaponSound("weapon/select")
    M40A BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    M40A E 0 A_JumpIfNoAmmo("Reload")
    M40A E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    M40A E 0 A_JumpIfInventory("Reloading",1,"Reload")
    M40A E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M40A E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40A E 1 A_WeaponReady
    loop 
  Aiming:
    M40C D 0 A_JumpIfInventory("Reloading",1,"Reload")    
    M40C D 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M40C D 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40C D 1 A_WeaponReady(WRF_NOBOB)
    M40C D 0 A_ZoomFactor(1.2)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    M40H ABC 2
    TNT1 AA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    M40A E 1 A_JumpIfInventory("Aiming",1,"FireAim")
    M40B A 0 A_JumpIfNoAmmo("ReloadStart")
    M40B A 0 A_FireCustomMissile("MP40Puff",0,0,6,1)
    M40B A 1 BRIGHT A_FireBullets (8, 8, -1, 65, "MP40Bullet", 1, 3000)
    M40B B 0 ACS_ExecuteAlways(600,0,500,100*random(2,4),0)		//Recoil script
    M40B B 1 BRIGHT A_GunFlash
    M40B CD 1 A_JumpIfNoAmmo("FireEmpty")
    M40B A 0 A_JumpIfInventory("MP40Ammo",1,2)
    M40B A 0 A_ReFire
    goto InHand
    M40B A 0 A_JumpIfNoAmmo(2)
    M40B A 0 A_ReFire
    goto InHand
    M40B A 0 A_GiveInventory("MP40Empty",1)		//Go to reload
    goto Reload
  FireEmpty:
    M40B ABC 1 
    M40B D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+8
  FireAim:
    M40C D 1 A_JumpIfNoAmmo("ReloadStart")
    M40D A 0 A_FireCustomMissile("MP40Puff",0,0,0,6)
    M40D A 1 BRIGHT A_FireBullets (0.75, 0.75, -1, 65, "MP40Bullet", 1, 5000)
    M40D B 0 ACS_ExecuteAlways(600,0,500,100*random(1,2),0)		//Recoil script
    M40D B 1 BRIGHT A_GunFlash
    M40D B 0 A_ZoomFactor(1.18)
    M40D CD 1 A_JumpIfNoAmmo("FireEmptyAim")
    M40D A 0 A_JumpIfInventory("MP40Ammo",1,2)
    M40D A 0 A_ReFire
    goto Aiming
    M40D A 0 A_JumpIfNoAmmo(2)
    M40D A 0 A_ReFire
    goto Aiming
    M40D A 0 A_GiveInventory("MP40Empty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    M40D ABC 1 
    M40D D 1 A_PlayWeaponSound("weapon/dryfire")
    goto FireAim+8
  Hold:
    M40A E 0 A_JumpIfInventory("Aiming",1,"HoldAim")
    M40A E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M40A E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    M40C D 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("MP40Ammo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    M40E A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    M40E A 0 A_WeaponReady(WRF_NOFIRE)
    M40E A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E B 0 A_WeaponReady(WRF_NOFIRE)
    M40E B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E C 0 A_PlaySoundEx("MP40/clipout","SoundSlot5")
    M40E C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    M40E C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E D 0 A_WeaponReady(WRF_NOFIRE)
    M40E D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E E 0 A_WeaponReady(WRF_NOFIRE)
    M40E E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E F 0 A_WeaponReady(WRF_NOFIRE)
    M40E F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E G 0 A_WeaponReady(WRF_NOFIRE)
    M40E G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E H 0 A_WeaponReady(WRF_NOFIRE)
    M40E H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E I 0 A_WeaponReady(WRF_NOFIRE)
    M40E I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E J 0 A_WeaponReady(WRF_NOFIRE)
    M40E J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E K 0 A_WeaponReady(WRF_NOFIRE)
    M40E K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("MP40Clip",1)
    TNT1 A 0 A_TakeInventory("MP40Ammo",1)
    TNT1 A 0 A_JumpIfInventory("MP40Clip",32,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("MP40Ammo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_JumpIfInventory("MP40Empty",1,"ReloadEndEmpty")
    M40E L 0 A_WeaponReady(WRF_NOFIRE)
    M40E L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E M 0 A_PlaySoundEx("MP40/clipin","SoundSlot5")
    M40E M 0 A_WeaponReady(WRF_NOFIRE)
    M40E M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E N 0 A_WeaponReady(WRF_NOFIRE)
    M40E N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40E O 0 A_WeaponReady(WRF_NOFIRE)
    M40E O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  ReloadEndEmpty:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_TakeInventory("MP40Empty",1)
    M40F A 0 A_WeaponReady(WRF_NOFIRE)
    M40F A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40F B 0 A_PlaySoundEx("MP40/clipin","SoundSlot5")
    M40F B 0 A_WeaponReady(WRF_NOFIRE)
    M40F B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40F C 0 A_WeaponReady(WRF_NOFIRE)
    M40F C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40F D 0 A_WeaponReady(WRF_NOFIRE)
    M40F D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40F E 0 A_PlaySoundEx("MP40/bolt","SoundSlot5")
    M40F E 0 A_WeaponReady(WRF_NOFIRE)
    M40F E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40F F 0 A_WeaponReady(WRF_NOFIRE)
    M40F F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M40F G 0 A_WeaponReady(WRF_NOFIRE)
    M40F G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    M40C A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    M40C A 0 A_ZoomFactor(1.2)		
    M40C ABCD 2
    M40C D 0 A_GiveInventory("Aiming",1)
    M40C D 0 ACS_ExecuteAlways(602,0,0,0,0)
    M40C D 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    M40C D 0 A_ZoomFactor(1.0)
    M40C DCBA 2
    M40C A 0 A_TakeInventory("Aiming",1)
    M40C A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    M40C D 1 A_ZoomFactor(1.0)
    M40C DCBA 1
    M40C A 0 A_TakeInventory("Aiming",1)
    M40C A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    M40G A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    M40G B 3
    M40G C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    M40G DEFGH 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGC ABCDEFG 3
    TNT1 A 15 
    FRGC H 3
    FRGC H 0 A_PlaySoundEx("stick/throw","SoundSlot5")
    FRGC I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGC I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGC I 0 A_TakeInventory("KnifeAttack",1)
    FRGC I 3 A_FireCustomMissile("StickGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor MP40Empty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor MP40Clip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 32
    ammo.backpackamount 0
    ammo.backpackmaxamount 32
    +IGNORESKILL
}

actor MP40Puff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("MP40/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("MP40/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("MP40/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("MP40/fire4","SoundSlot6")
    stop
  }
}

actor MP40Bullet : HHBulletPuff
{
  damagetype "MP40"
}
