actor "Scoped Mosin-Nagant" : RGUWeapon
{
  obituary "\cf%k \cc>> \cgScoped Mosin-Nagant \cc>> \cf%o"
  inventory.pickupmessage "Scoped Mosin-Nagant"  
  weapon.selectionorder 16
  weapon.ammotype "NagantClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "NagantAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 5
  +NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",4)	//Determines player speed - 4 means 30% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    NASA A 2 A_PlayWeaponSound("weapon/select")
    NASA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    NASA E 0 A_JumpIfNoAmmo("Reload")
    NASA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    NASA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    NASA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    NASA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASA E 0 A_JumpIfInventory("NagantSClosed",1,1)
    NASA E 1 A_WeaponReady
    loop 
  Aiming:
    SCOP C 0 A_JumpIfInventory("Reloading",1,"Reload")    
    SCOP C 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    SCOP C 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SCOP C 1 A_WeaponReady(WRF_NOBOB)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 4 means 30% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
    NASH ABCD 2
    TNT1 A 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    NASA E 0 A_JumpIfInventory("Reloading",1,"ReloadEnd")
    NASA E 1 A_JumpIfInventory("Aiming",1,"FireAim")
    NASA E 1 A_JumpIfInventory("NagantSBolt",1,"Bolt")
    NASB A 0 A_JumpIfNoAmmo("ReloadStart")
    NASB A 0 A_TakeInventory("NagantSClosed",1)
    NASB A 0 A_FireCustomMissile("NagantPuff2",0,0,4,2)
    NASB A 1 BRIGHT A_FireBullets (15, 15, -1, 100, "NagantBullet3", 1, 3000)
    NASB B 0 ACS_ExecuteAlways(600,0,1500,150*random(2,4),0)	//Recoil script
    NASB B 1 BRIGHT A_GunFlash
    NASB CDEE 1 A_JumpIfNoAmmo("FireEmpty")
    NASB E 5 A_WeaponReady(WRF_NOFIRE)
	NASB E 0 A_GiveInventory("NagantSBolt",1)
    NASB E 5 A_WeaponReady(WRF_NOFIRE)
    goto InHand
    NASB A 0 A_JumpIfNoAmmo(2)
    goto InHand
    NASB A 0 A_GiveInventory("NagantEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    NASB CDEF 1
    goto Fire+17
  FireAim:
    SCOP C 1 A_JumpIfInventory("NagantSBolt",1,"BoltAim")  
    SCOP C 1 A_JumpIfNoAmmo("ReloadStart")
    SCOP C 0 A_TakeInventory("NagantSClosed",1)
    SCOP C 0 A_FireCustomMissile("NagantPuff2",0,0,0,4)
    SCOP C 1 BRIGHT A_FireBullets (0.1, 0.1, 2, 100, "NagantBullet2", 1, 9001) //45, 90, 135, 180, 225, 270
    SCOP C 0 ACS_ExecuteAlways(600,0,1500,150*random(1,2),0)		//Recoil script
    SCOP C 1 BRIGHT A_GunFlash
    SCOP C 4 A_ZoomFactor(1.5)
    SCOP C 1 A_JumpIfNoAmmo("FireEmptyAim")
    SCOP C 3 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB|WRF_NOFIRE)
    SCOP C 0 A_ZoomFactor(3.5)
    SCOP C 0 A_GiveInventory("NagantSBolt",1)
    SCOP A 0 A_JumpIfInventory("NagantAmmo",1,2)
    SCOP A 0 A_ReFire
    goto InHand
    SCOP C 0 A_JumpIfNoAmmo(2)
    goto InHand
    SCOP C 0 A_GiveInventory("NagantEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    SCOP CCCC 1
    goto Fire+17
  Hold:
    NASA E 0 A_JumpIfInventory("Aiming",1,"HoldAim")	//Don't need a hold because of the already long rechamber time
    NASA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    NASA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    NASC E 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("NagantAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    NASE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    NASE N 0 A_GiveInventory("Reloading",1)
    NASE A 0 A_WeaponReady(WRF_NOFIRE)
    NASE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE B 0 A_TakeInventory("NagantSClosed",1)
    NASE B 0 A_PlaySoundEx("kar98k/open","SoundSlot5")
    NASE B 0 A_WeaponReady(WRF_NOFIRE)
    NASE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    NASE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE D 0 A_WeaponReady(WRF_NOFIRE)
    NASE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE E 0 A_WeaponReady(WRF_NOFIRE)
    NASE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE F 0 A_WeaponReady(WRF_NOFIRE)
    NASE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE G 0 A_WeaponReady(WRF_NOFIRE)
    NASE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE H 0 A_WeaponReady(WRF_NOFIRE)
    NASE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("NagantClip",1)
    TNT1 A 0 A_TakeInventory("NagantAmmo",1)
    TNT1 A 0 A_TakeInventory("NagantSBolt",1)
    NASE I 0 A_WeaponReady(WRF_NOFIRE)
    NASE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE J 0 A_PlaySoundEx("spring/shell","SoundSlot5")
    NASE J 0 A_WeaponReady(WRF_NOBOB)
    NASE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE K 0 A_WeaponReady(WRF_NOBOB)
    NASE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE L 0 A_WeaponReady(WRF_NOBOB)
    NASE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_JumpIfInventory("NagantClip",5,"ReloadEnd")
    NASE G 0 A_WeaponReady(WRF_NOBOB)
    NASE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE H 0 A_WeaponReady(WRF_NOBOB)
    NASE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_JumpIfInventory("NagantAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    NASE M 0 A_WeaponReady(WRF_NOFIRE)
    NASE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE N 0 A_GiveInventory("NagantSClosed",1)
    NASE N 0 A_PlaySoundEx("kar98k/close","SoundSlot5")
    NASE N 0 A_WeaponReady(WRF_NOFIRE)
    NASE N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NASE O 0 A_WeaponReady(WRF_NOFIRE)
    NASE O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto InHand
  AltFire:	//Aim
    NASA E 0 A_JumpIfInventory("Reloading",1,"ReloadEnd")
    NASC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    NASC ABC 2
    NASC A 0 A_ZoomFactor(3.5,ZOOM_INSTANT)
    NASC E 0 A_GiveInventory("Aiming",1)
    NASC E 0 ACS_ExecuteAlways(602,0,1,0,0)
    NASC E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    NASC C 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
    NASC CBA 2
    NASC A 0 A_TakeInventory("Aiming",1)
    NASC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    NASC C 1 A_ZoomFactor(1.0,ZOOM_INSTANT)
    NASC CBA 1
    NASC A 0 A_TakeInventory("Aiming",1)
    NASC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0,ZOOM_INSTANT)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    NASG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    NASG B 3
    NASG C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    NASG DE 3
    TNT1 A 3
    NASG FG 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGD ABCDEFG 3
    TNT1 A 15 
    FRGD H 3
    FRGD H 0 A_PlaySoundEx("stick/throw","SoundSlot5")
    FRGD I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGD I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGD I 0 A_TakeInventory("KnifeAttack",1)
    FRGD I 3 A_FireCustomMissile("RussianGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  Bolt:
	NASB H 0 A_JumpIfInventory("Aiming",1,"BoltAim")
	NASB H 0 A_GiveInventory("NagantSClosed", 1)
	NASB H 0 A_TakeInventory("NagantSBolt",1)
    NASB H 5 A_PlayWeaponSound("kar98k/chamber")
    NASB IJKL 5 A_WeaponReady(WRF_NOFIRE)
	goto InHand
  BoltAim:
    NASC C 0 A_ZoomFactor(1.0,ZOOM_INSTANT) //zoom out and show the rifle before bolt.
    NASC CBA 2
    NASC A 0 A_TakeInventory("Aiming",1)
    NASC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    NASB H 0 A_GiveInventory("NagantSClosed", 1)
	NASB H 0 A_TakeInventory("NagantSBolt",1)
    NASB H 5 A_PlayWeaponSound("kar98k/chamber")
    NASB IJKL 5 A_WeaponReady(WRF_NOFIRE)
    goto InHand
  }
}

actor NagantPuff2
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuffShotgun_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash2", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("nagant/sniper1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("nagant/sniper3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("nagant/sniper2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("nagant/sniper4","SoundSlot6")
    stop
  }
}

actor NagantBullet2 : HHBulletPuff
{
  damagetype "Nagant2"
}

actor NagantSBolt : Inventory //If this is in the the players inventory the rifle needs to be bolted.
{
	inventory.amount 0
    inventory.maxamount 1
}

actor NagantSClosed : Inventory //Custom closed actor to prevent conflict with Nagant.
{
	inventory.amount 0
    inventory.maxamount 1
}
