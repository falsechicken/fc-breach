actor "M1 Garand" : RGUWeapon
{
  obituary "\cf%k \cc>> \crM1 Garand \cc>> \cf%o"
  inventory.pickupmessage "M1 Garand"  
  weapon.selectionorder 18
  weapon.ammotype "M1GarandClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "M1GarandAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 8
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",3)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    M1GA A 2 A_PlayWeaponSound("weapon/select")
    M1GA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    M1GA E 0 A_JumpIfNoAmmo("Reload")
    M1GA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    M1GA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    M1GA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M1GA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1GA E 1 A_WeaponReady
    loop 
  Aiming:
    M1GC E 0 A_JumpIfInventory("Reloading",1,"Reload")    
    M1GC E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M1GC E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1GC E 1 A_WeaponReady(WRF_NOBOB)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    M1GH ABCD 2
    TNT1 A 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    M1GA E 0 A_JumpIfInventory("Aiming",1,"FireAim")
    M1GA E 1 A_JumpIfNoAmmo("ReloadStart")
    M1GB A 0 A_FireBullets (12, 12, -1, 85, "M1GarandBullet", 1, 3000)
    M1GB B 0 A_JumpIfNoAmmo("FireEmpty")
    M1GB A 1 BRIGHT A_FireCustomMissile("M1GarandPuff",0,0,4,4)
    M1GB B 0 ACS_ExecuteAlways(600,0,1000,50*random(2,3),0)	//Recoil script - 750 recoil, don't revert aim
    M1GB BC 1 BRIGHT A_GunFlash
    M1GB DEF 1 A_WeaponReady
    M1GA EEEEE 1 A_WeaponReady
    M1GB A 0 A_JumpIfInventory("M1GarandAmmo",1,2)
    M1GB A 0 A_ReFire
    goto InHand
    M1GB A 0 A_JumpIfNoAmmo(2)
    M1GB A 0 A_ReFire
    goto InHand
    M1GB A 0 A_GiveInventory("M1GarandEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    M1GB A 0 A_FireCustomMissile("M1GarandPuff",0,0,4,4)
    M1GB A 1 BRIGHT A_FireCustomMissile("M1GarandPuff2",0,0,4,4)  //Ping sound
    M1GE B 0 ACS_ExecuteAlways(600,0,1200,600,0)		//Recoil script - 1200 recoil, don't revert aim
    M1GE A 2 BRIGHT A_GunFlash
    M1GE BCD 2
    M1GE A 0 A_JumpIfInventory("M1GarandAmmo",1,2)
    M1GE A 0 A_ReFire
    goto InHand
    M1GE A 0 A_JumpIfNoAmmo(2)
    M1GE A 0 A_ReFire
    goto InHand
    M1GE A 0 A_GiveInventory("M1GarandEmpty",1)		//Go to reload
    goto Reload
  FireAim:
    M1GC E 1 A_JumpIfNoAmmo("ReloadStart")
    M1GD A 0 A_FireBullets (0.35, 0.35, -1, 90, "M1GarandBullet", 1, 9001)
    M1GD B 0 A_JumpIfNoAmmo("FireEmptyAim")
    M1GD A 1 BRIGHT A_FireCustomMissile("M1GarandPuff",0,0,0,5)
    M1GD B 0 ACS_ExecuteAlways(600,0,1000,50*random(1,2),0)	//Recoil script
    M1GD BC 1 BRIGHT A_GunFlash
    M1GD C 1 A_ZoomFactor(1.18)
    M1GD DEF 1 A_WeaponReady(WRF_NOBOB)
    M1GC EEEEE 1 A_WeaponReady(WRF_NOBOB)
    M1GC E 0 A_ZoomFactor(1.2)
    M1GD A 0 A_JumpIfInventory("M1GarandAmmo",1,2)
    M1GD A 0 A_ReFire
    goto InHand
    M1GD A 0 A_JumpIfNoAmmo(2)
    M1GD A 0 A_ReFire
    goto InHand
    M1GD A 0 A_GiveInventory("M1GarandEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    M1GD A 0 A_FireCustomMissile("M1GarandPuff",0,0,0,5)
    M1GD A 1 BRIGHT A_FireCustomMissile("M1GarandPuff2",0,0,0,5)  //Ping sound
    M1GE E 0 ACS_ExecuteAlways(600,0,1200,900,0)		//Recoil script - 1200 recoil, revert aim
    M1GE E 2 BRIGHT A_GunFlash
    M1GE FG 2
    M1GE H 2 A_ZoomFactor(1.0)
    M1GE I 2 A_TakeInventory("Aiming",1)
    M1GE A 0 ACS_ExecuteAlways(602,0,0,0,0)
    M1GE A 0 A_JumpIfInventory("M1GarandAmmo",1,2)
    M1GE A 0 A_ReFire
    goto InHand
    M1GE A 0 A_JumpIfNoAmmo(2)
    M1GE A 0 A_ReFire
    goto InHand
    M1GE A 0 A_GiveInventory("M1GarandEmpty",1)		//Go to reload
    goto Reload
  Hold:
    M1GA E 3 A_JumpIfInventory("Aiming",1,"HoldAim")
    M1GA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M1GA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1GA E 0 A_JumpIfInventory("IsBot",1,"Fire")
    M1GA E 1 A_ReFire
    goto InHand
  HoldAim:
    M1GC E 3
    M1GA E 0 A_JumpIfInventory("IsBot",1,"FireAim")
    M1GC E 1 A_ReFire
    goto Aiming
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("M1GarandAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    M1GF A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    M1GF A 0 A_WeaponReady(WRF_NOFIRE)
    M1GF A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1GF B 0 A_WeaponReady(WRF_NOFIRE)
    M1GF B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1GF C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    M1GF C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1GF D 0 A_WeaponReady(WRF_NOFIRE)
    M1GF D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1GF E 0 A_WeaponReady(WRF_NOFIRE)
    M1GF E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1GF F 0 A_WeaponReady(WRF_NOFIRE)
    M1GF F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1GF G 0 A_PlaySoundEx("m1garand/clipin","SoundSlot5")
    M1GF G 0 A_WeaponReady(WRF_NOFIRE)
    M1GF G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1GF H 0 A_WeaponReady(WRF_NOFIRE)
    M1GF H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M1GF I 0 A_WeaponReady(WRF_NOFIRE)
    M1GF I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("M1GarandClip",1)
    TNT1 A 0 A_TakeInventory("M1GarandAmmo",1)
    TNT1 A 0 A_JumpIfInventory("M1GarandClip",8,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("M1GarandAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_TakeInventory("M1GarandEmpty",1)
    M1GF J 0 A_WeaponReady(WRF_NOFIRE)
    M1GF J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    M1GC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    M1GC A 0 A_ZoomFactor(1.2)
    M1GC ABCDE 2
    M1GC E 0 A_GiveInventory("Aiming",1)
    M1GC E 0 ACS_ExecuteAlways(602,0,0,0,0)
    M1GC E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    M1GC E 0 A_ZoomFactor(1.0)
    M1GC EDCBA 2
    M1GC A 0 A_TakeInventory("Aiming",1)
    M1GC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    M1GC E 1 A_ZoomFactor(1.0)
    M1GC EDCBA 1
    M1GC A 0 A_TakeInventory("Aiming",1)
    M1GC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    M1GG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    M1GG B 3
    M1GG C 3 A_FireCustomMissile("MeleeAttack",0,0,0,0)
    M1GG DEF 3
    TNT1 A 3
    M1GG G 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGA A 0 A_JumpIfInventory("IsBritish",1,"MillsGrenade")
    FRGA ABCDEF 3
    FRGA G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGA H 3
    FRGA H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGA I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGA I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGA I 0 A_TakeInventory("KnifeAttack",1)
    FRGA I 3 A_FireCustomMissile("FragGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  MillsGrenade:
    FRGE ABCDEF 3
    FRGE G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGE H 3
    FRGE H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGE I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGE I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGE I 0 A_TakeInventory("KnifeAttack",1)
    FRGE I 3 A_FireCustomMissile("MillsBomb",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor M1GarandEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor M1GarandClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 8
    ammo.backpackamount 0
    ammo.backpackmaxamount 8
    +IGNORESKILL
}

actor M1GarandPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("m1garand/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("m1garand/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("m1garand/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("m1garand/fire4","SoundSlot6")
    stop
  }
}

actor M1GarandPuff2
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_PlaySoundEx("m1garand/empty1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("m1garand/empty2","SoundSlot6")
    stop
  }
}

actor M1GarandBullet : HHBulletPuff
{
  damagetype "Garand"
}
