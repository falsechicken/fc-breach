actor "BAR" : RGUWeapon
{
  obituary "\cf%k \cc>> \cqBAR \cc>> \cf%o"
  inventory.pickupmessage "BAR"  
  weapon.selectionorder 15
  weapon.ammotype "BARClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "BARAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 20
  -NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",4)	//Determines player speed - 4 means 30% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    BARA A 2 A_PlayWeaponSound("weapon/select")
    BARA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    BARA E 0 A_JumpIfNoAmmo("Reload")
    BARA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    BARA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    BARA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    BARA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARA E 1 A_WeaponReady
    loop 
  Aiming:
    BARC E 0 A_JumpIfInventory("Reloading",1,"Reload")    
    BARC E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    BARC E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARC E 1 A_WeaponReady(WRF_NOBOB)
    BARC E 0 A_ZoomFactor(1.3)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 4 means 30% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    BARH ABC 2
    TNT1 AA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    BARA E 0 A_JumpIfInventory("Aiming",1,"FireAim")
    BARB A 0 A_JumpIfNoAmmo("ReloadStart")
    BARB A 0 A_FireCustomMissile("BARPuff",0,0,4,1)
    BARB A 1 BRIGHT A_FireBullets (20, 20, -1, 65, "BARBullet", 1, 3000)
    BARB B 0 ACS_ExecuteAlways(600,0,900,150*random(2,4),0)		//Recoil script
    BARB B 1 BRIGHT A_GunFlash
    BARB CD 1 A_JumpIfNoAmmo("FireEmpty")
    BARB A 0 A_JumpIfInventory("BARAmmo",1,2)
    BARB A 0 A_ReFire
    goto InHand
    BARB A 0 A_JumpIfNoAmmo(2)
    BARB A 0 A_ReFire
    goto InHand
    BARB A 0 A_GiveInventory("BAREmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    BARB ABC 1 
    BARB D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+8
  FireAim:
    BARC D 0 A_JumpIfNoAmmo("ReloadStart")
    BARD A 0 A_FireCustomMissile("BARPuff",0,0,0,6)
    BARD A 1 BRIGHT A_FireBullets (0.35, 0.35, -1, 65, "BARBullet", 1, 6000)
    BARD B 0 ACS_ExecuteAlways(600,0,900,150*random(1,3),0)		//Recoil script
    BARD B 1 BRIGHT A_GunFlash
    BARD B 0 A_ZoomFactor(1.28)
    BARD CD 1 A_JumpIfNoAmmo("FireEmptyAim")
    BARD A 0 A_JumpIfInventory("BARAmmo",1,2)
    BARD A 0 A_ReFire
    goto Aiming
    BARD A 0 A_JumpIfNoAmmo(2)
    BARD A 0 A_ReFire
    goto Aiming
    BARD A 0 A_GiveInventory("BAREmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    BARD ABC 1 
    BARD D 1 A_PlayWeaponSound("weapon/dryfire")
    goto FireAim+8
  Hold:
    BARA E 0 A_JumpIfInventory("Aiming",1,"HoldAim")
    BARA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    BARA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    BARC D 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("BARAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    BARE A 0 A_ZoomFactor(1.0)
    BARE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    BARE A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE B 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE C 0 A_JumpIfInventory("BARClip",1,8)
    BARE C 0 A_PlaySoundEx("bar/bolt","SoundSlot5")
    BARE C 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)			//This apparent "messy" setup allows for interruption at all times
    BARE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE D 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE E 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE F 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE G 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE H 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE I 0 A_PlaySoundEx("bar/clipout","SoundSlot5")
    BARE I 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE J 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE K 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE L 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE M 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE N 0 A_PlaySoundEx("bar/clipin","SoundSlot5")
    BARE N 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE O 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE P 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE P 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE Q 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE Q 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("BARClip",1)
    TNT1 A 0 A_TakeInventory("BARAmmo",1)
    TNT1 A 0 A_JumpIfInventory("BARClip",20,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("BARAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    BARE R 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE R 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE S 0 A_PlaySoundEx("bar/hit","SoundSlot5")
    BARE S 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE S 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE T 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE T 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE U 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE U 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    BARE V 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    BARE V 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto InHand
  AltFire:	//Aim
    BARC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    BARC A 0 A_ZoomFactor(1.3)		
    BARC ABCDE 2
    BARC E 0 A_GiveInventory("Aiming",1)
    BARC E 0 ACS_ExecuteAlways(602,0,0,0,0)
    BARC E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    BARC E 0 A_ZoomFactor(1.0)
    BARC EDCBA 2
    BARC A 0 A_TakeInventory("Aiming",1)
    BARC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    BARC E 1 A_ZoomFactor(1.0)
    BARC EDCBA 1
    BARC A 0 A_TakeInventory("Aiming",1)
    BARC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    BARG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    BARG BC 3
    BARG D 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    BARG EFGHI 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGA ABCDEF 3
    FRGA G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGA H 3
    FRGA H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGA I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGA I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGA I 0 A_TakeInventory("KnifeAttack",1)
    FRGA I 3 A_FireCustomMissile("FragGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor BAREmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor BARShots : Inventory
{
    inventory.amount 0
    inventory.maxamount 2
}
actor BARClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 20
    ammo.backpackamount 0
    ammo.backpackmaxamount 20
    +IGNORESKILL
}

actor BARPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 1
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuffShotgun_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash2", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("bar/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("bar/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("bar/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("bar/fire4","SoundSlot6")
    stop
  }
}

actor BARBullet : HHBulletPuff
{
  damagetype "BAR"
}
