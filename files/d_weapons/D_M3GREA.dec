actor "M3 Grease Gun" : RGUWeapon
{
  obituary "\cf%k \cc>> \cdM3 \cc>> \cf%o"
  inventory.pickupmessage "M3 Grease Gun"  
  weapon.selectionorder 20
  weapon.ammotype "M3GGClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "M3GGAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 32
  -NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",2)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    M3GA A 2 A_PlayWeaponSound("weapon/select")
    M3GA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    M3GA E 0 A_JumpIfNoAmmo("Reload")
    M3GA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    M3GA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    M3GA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M3GA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GA E 1 A_WeaponReady
    loop 
  Aiming:
    M3GC D 0 A_JumpIfInventory("Reloading",1,"Reload")    
    M3GC D 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M3GC D 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GC D 1 A_WeaponReady(WRF_NOBOB)
    M3GC D 0 A_ZoomFactor(1.2)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 2 means 10% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    M3GH ABC 2
    TNT1 AA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    M3GA E 1 A_JumpIfInventory("Aiming",1,"FireAim")
    M3GB A 0 A_JumpIfNoAmmo("ReloadStart")
    M3GB A 0 A_FireCustomMissile("M3GGPuff",0,0,6,1)
    M3GB A 1 BRIGHT A_FireBullets (8, 8, -1, 65, "M3GGBullet", 1, 3000)
    M3GB B 0 ACS_ExecuteAlways(600,0,500,75*random(2,4),0)		//Recoil script
    M3GB B 1 BRIGHT A_GunFlash
    M3GB CD 1 A_JumpIfNoAmmo("FireEmpty")
    M3GB A 0 A_JumpIfInventory("M3GGAmmo",1,2)
    M3GB A 0 A_ReFire
    goto InHand
    M3GB A 0 A_JumpIfNoAmmo(2)
    M3GB A 0 A_ReFire
    goto InHand
    M3GB A 0 A_GiveInventory("M3GGEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    M3GB ABC 1 
    M3GB D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+8
  FireAim:
    M3GC D 1 A_JumpIfNoAmmo("ReloadStart")
    M3GD A 0 A_FireCustomMissile("M3GGPuff",0,0,0,6)
    M3GD A 1 BRIGHT A_FireBullets (0.75, 0.75, -1, 65, "M3GGBullet", 1, 5000)
    M3GD B 0 ACS_ExecuteAlways(600,0,500,75*random(1,2),0)	//Recoil script
    M3GD B 1 BRIGHT A_GunFlash
    M3GD B 0 A_ZoomFactor(1.18)
    M3GD CD 1 A_JumpIfNoAmmo("FireEmptyAim")
    M3GD A 0 A_JumpIfInventory("M3GGAmmo",1,2)
    M3GD A 0 A_ReFire
    goto Aiming
    M3GD A 0 A_JumpIfNoAmmo(2)
    M3GD A 0 A_ReFire
    goto Aiming
    M3GD A 0 A_GiveInventory("M3GGEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    M3GD ABC 1 
    M3GD D 1 A_PlayWeaponSound("weapon/dryfire")
    goto FireAim+8
  Hold:
    M3GA E 0 A_JumpIfInventory("Aiming",1,"HoldAim")
    M3GA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    M3GA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    M3GC D 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("M3GGAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    M3GE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    M3GE A 0 A_WeaponReady(WRF_NOFIRE)
    M3GE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE B 0 A_WeaponReady(WRF_NOFIRE)
    M3GE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE C 0 A_PlaySoundEx("m3gg/clipout","SoundSlot5")
    M3GE C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    M3GE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE D 0 A_WeaponReady(WRF_NOFIRE)
    M3GE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE E 0 A_WeaponReady(WRF_NOFIRE)
    M3GE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE F 0 A_WeaponReady(WRF_NOFIRE)
    M3GE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE G 0 A_WeaponReady(WRF_NOFIRE)
    M3GE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE H 0 A_WeaponReady(WRF_NOFIRE)
    M3GE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE I 0 A_WeaponReady(WRF_NOFIRE)
    M3GE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE J 0 A_WeaponReady(WRF_NOFIRE)
    M3GE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE K 0 A_PlaySoundEx("m3gg/clipin","SoundSlot5")
    M3GE K 0 A_WeaponReady(WRF_NOFIRE)
    M3GE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("M3GGClip",1)
    TNT1 A 0 A_TakeInventory("M3GGAmmo",1)
    TNT1 A 0 A_JumpIfInventory("M3GGClip",32,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("M3GGAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_JumpIfInventory("M3GGEmpty",1,"ReloadEndEmpty")
    M3GF A 0 A_WeaponReady(WRF_NOFIRE)
    M3GF A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GF B 0 A_WeaponReady(WRF_NOFIRE)
    M3GF B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GF C 0 A_WeaponReady(WRF_NOFIRE)
    M3GF C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  ReloadEndEmpty:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_TakeInventory("M3GGEmpty",1)
    M3GE L 0 A_WeaponReady(WRF_NOFIRE)
    M3GE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE M 0 A_WeaponReady(WRF_NOFIRE)
    M3GE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE N 0 A_WeaponReady(WRF_NOFIRE)
    M3GE N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE O 0 A_PlaySoundEx("m3gg/bolt","SoundSlot5")
    M3GE O 0 A_WeaponReady(WRF_NOFIRE)
    M3GE O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE P 0 A_WeaponReady(WRF_NOFIRE)
    M3GE P 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE Q 0 A_WeaponReady(WRF_NOFIRE)
    M3GE Q 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE R 0 A_WeaponReady(WRF_NOFIRE)
    M3GE R 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    M3GE S 0 A_WeaponReady(WRF_NOFIRE)
    M3GE S 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    M3GC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    M3GC A 0 A_ZoomFactor(1.2)		
    M3GC ABCD 2
    M3GC D 0 A_GiveInventory("Aiming",1)
    M3GC D 0 ACS_ExecuteAlways(602,0,0,0,0)
    M3GC D 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    M3GC D 0 A_ZoomFactor(1.0)
    M3GC DCBA 2
    M3GC A 0 A_TakeInventory("Aiming",1)
    M3GC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    M3GC D 1 A_ZoomFactor(1.0)
    M3GC DCBA 1
    M3GC A 0 A_TakeInventory("Aiming",1)
    M3GC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    M3GG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    M3GG B 3
    M3GG C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    M3GG DEFGH 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGA ABCDEF 3
    FRGA G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGA H 3
    FRGA H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGA I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGA I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGA I 0 A_TakeInventory("KnifeAttack",1)
    FRGA I 3 A_FireCustomMissile("FragGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor M3GGEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor M3GGClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 32
    ammo.backpackamount 0
    ammo.backpackmaxamount 32
    +IGNORESKILL
}

actor M3GGPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 1
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("m3gg/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("m3gg/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("m3gg/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("m3gg/fire4","SoundSlot6")
    stop
  }
}

actor M3GGBullet : HHBulletPuff
{
  damagetype "M3"
}
