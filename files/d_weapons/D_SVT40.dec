actor "SVT-40" : RGUWeapon
{
  obituary "\cf%k \cc>> \crSVT-40 \cc>> \cf%o"
  inventory.pickupmessage "SVT-40"  
  weapon.selectionorder 18
  weapon.ammotype "SVTClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "SVTAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 10
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",3)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    SVTA A 2 A_PlayWeaponSound("weapon/select")
    SVTA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    SVTA E 0 A_JumpIfNoAmmo("Reload")
    SVTA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    SVTA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    SVTA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    SVTA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTA E 1 A_WeaponReady
    loop 
  Aiming:
    SVTC E 0 A_JumpIfInventory("Reloading",1,"Reload")    
    SVTC E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    SVTC E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTC E 1 A_WeaponReady(WRF_NOBOB)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    SVTH ABCD 2
    TNT1 A 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    SVTA E 1 A_JumpIfInventory("Aiming",1,"FireAim")
    SVTB A 0 A_JumpIfNoAmmo("ReloadStart")
    SVTB A 0 A_FireCustomMissile("SVTPuff",0,0,4,2)
    SVTB A 1 BRIGHT A_FireBullets (12, 12, -1, 65, "SVTBullet", 1, 3000)
    SVTB B 0 ACS_ExecuteAlways(600,0,1000,100*random(2,3),0)	//Recoil script 
    SVTB B 1 BRIGHT A_GunFlash
    SVTB C 1 A_JumpIfNoAmmo("FireEmpty")
    SVTB DE 1 A_WeaponReady
    SVTA EEEE 1 A_WeaponReady
    SVTB A 0 A_JumpIfInventory("SVTAmmo",1,2)
    SVTB A 0 A_ReFire
    goto InHand
    SVTB A 0 A_JumpIfNoAmmo(2)
    SVTB A 0 A_ReFire
    goto InHand
    SVTB A 0 A_GiveInventory("SVTEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    SVTB CDE 1
    goto Fire+13
  FireAim:
    SVTC E 1 A_JumpIfNoAmmo("ReloadStart")
    SVTD A 0 A_FireCustomMissile("SVTPuff",0,0,0,4)
    SVTD A 1 BRIGHT A_FireBullets (0.75, 0.75, -1, 65, "SVTBullet", 1, 6000)
    SVTD B 0 ACS_ExecuteAlways(600,0,1000,100*random(1,2),0)		//Recoil script
    SVTD B 1 BRIGHT A_GunFlash
    SVTD B 1 A_ZoomFactor(1.18)
    SVTD C 1 A_JumpIfNoAmmo("FireEmptyAim")
    SVTD DE 1 A_WeaponReady
    SVTC EEEE 1 A_WeaponReady
    SCTC E 0 A_ZoomFactor(1.2)
    SVTD A 0 A_JumpIfInventory("SVTAmmo",1,2)
    SVTD A 0 A_ReFire
    goto InHand
    SVTD A 0 A_JumpIfNoAmmo(2)
    SVTD A 0 A_ReFire
    goto InHand
    SVTD A 0 A_GiveInventory("SVTEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    SVTD CDE 1
    goto Fire+13
  Hold:
    SVTA E 3 A_JumpIfInventory("Aiming",1,"HoldAim")
    SVTA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    SVTA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTA E 0 A_JumpIfInventory("IsBot",1,"Fire")
    SVTA E 1 A_ReFire
    goto InHand
  HoldAim:
    SVTC E 3
    SVTC E 0 A_JumpIfInventory("IsBot",1,"FireAim")
    SVTC E 1 A_ReFire
    goto Aiming
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("SVTAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    SVTE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    SVTE A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE B 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE C 0 A_PlaySoundEx("svt/clipout","SoundSlot5")
    SVTE C 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)         //This apparent "messy" setup allows for interruption at all times
    SVTE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE D 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE E 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE F 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE G 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE H 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE I 0 A_PlaySoundEx("svt/clipin","SoundSlot5")
    SVTE I 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE J 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE K 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE L 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE M 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE N 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE O 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE O 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("SVTClip",1)
    TNT1 A 0 A_TakeInventory("SVTAmmo",1)
    TNT1 A 0 A_JumpIfInventory("SVTClip",10,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("SVTAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    SVTE P 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE P 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE Q 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE Q 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    SVTE R 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    SVTE R 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    SVTC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    SVTC A 0 A_ZoomFactor(1.2)		
    SVTC ABCDE 2
    SVTC E 0 A_GiveInventory("Aiming",1)
    SVTC E 0 ACS_ExecuteAlways(602,0,0,0,0)
    SVTC E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    SVTC E 0 A_ZoomFactor(1.0)
    SVTC EDCBA 2
    SVTC A 0 A_TakeInventory("Aiming",1)
    SVTC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    SVTC E 1 A_ZoomFactor(1.0)
    SVTC EDCBA 1
    SVTC A 0 A_TakeInventory("Aiming",1)
    SVTC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    SVTG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    SVTG B 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    SVTG CDE 3
    TNT1 A 3
    SVTG FG 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGD ABCDEFG 3
    TNT1 A 15 
    FRGD H 3
    FRGD H 0 A_PlaySoundEx("stick/throw","SoundSlot5")
    FRGD I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGD I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGD I 0 A_TakeInventory("KnifeAttack",1)
    FRGD I 3 A_FireCustomMissile("RussianGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor SVTEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor SVTClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 10
    ammo.backpackamount 0
    ammo.backpackmaxamount 10
    +IGNORESKILL
}

actor SVTPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("SVT/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("SVT/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("SVT/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("SVT/fire4","SoundSlot6")
    stop
  }
}

actor SVTBullet : HHBulletPuff
{
  damagetype "SVT"
}
