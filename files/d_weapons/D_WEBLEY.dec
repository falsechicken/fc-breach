actor "Webley" : RGUWeapon
{
  obituary "\cf%k \cc>> \ciWebley \cc>> \cf%o"
  inventory.pickupmessage "Webley"  
  weapon.selectionorder 21
  weapon.ammotype "WebleyClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "WebleyAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 6
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",1)	//Determines player speed - 1 means normal speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    WEBA A 2 A_PlayWeaponSound("weapon/select")
    WEBA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    WEBA E 0 A_JumpIfNoAmmo("Reload")
    WEBA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    WEBA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    WEBA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    WEBA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBA E 1 A_WeaponReady
    loop 
  Aiming:
    WEBC D 0 A_JumpIfInventory("Reloading",1,"Reload")    
    WEBC D 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    WEBC D 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBC D 1 A_WeaponReady(WRF_NOBOB)
    WEBC D 0 A_ZoomFactor(1.0)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 1 means normal speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    WEBA EDCBA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    WEBA E 0 A_JumpIfInventory("Aiming",1,"FireAim")
    WEBA E 1 A_JumpIfNoAmmo("ReloadStart")
    WEBB A 0 A_FireCustomMissile("WebleyPuff",0,0,12,1)
    WEBB A 1 BRIGHT A_FireBullets (6, 6, -1, 50, "WebleyBullet", 1, 2300)
    WEBB B 0 ACS_ExecuteAlways(600,0,600,30*random(2,4),0)	//Recoil script
    WEBB B 1 BRIGHT A_GunFlash
    WEBB B 0 A_JumpIfNoAmmo("FireEmpty")
    WEBB CDEFG 1 A_WeaponReady
    WEBA E 1 A_WeaponReady
    WEBB A 0 A_JumpIfInventory("WebleyAmmo",1,2)
    WEBB A 0 A_ReFire
    goto InHand
    WEBB A 0 A_JumpIfNoAmmo(2)
    WEBB A 0 A_ReFire
    goto InHand
    WEBB A 0 A_GiveInventory("WebleyEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    WEBB CDEFG 1 
    WEBA E 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+13
  FireAim:
    WEBC D 1 A_JumpIfNoAmmo("ReloadStart")
    WEBD A 0 A_FireCustomMissile("WebleyPuff",0,0,0,6)
    WEBD A 1 BRIGHT A_FireBullets (0.5, 0.5, -1, 50, "WebleyBullet", 1, 2300)
    WEBD B 0 ACS_ExecuteAlways(600,0,600,30*random(1,2),0) //Recoil script
    WEBD B 1 BRIGHT A_GunFlash
    WEBD B 1 A_ZoomFactor(0.98)
    WEBD B 0 A_JumpIfNoAmmo("FireEmptyAim")
    WEBD CDEFG 1 A_WeaponReady
    WEBC D 1 A_WeaponReady
    WEBD A 0 A_JumpIfInventory("WebleyAmmo",1,2)
    WEBD A 0 A_ReFire
    goto Aiming
    WEBD A 0 A_JumpIfNoAmmo(2)
    WEBD A 0 A_ReFire
    goto Aiming
    WEBD A 0 A_GiveInventory("WebleyEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    WEBD CDEFG 1 
    WEBC D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+13
  Hold:
    WEBA E 3 A_JumpIfInventory("Aiming",1,"HoldAim")
    WEBA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    WEBA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBA E 0 A_JumpIfInventory("IsBot",1,"Fire")
    WEBA E 1 A_ReFire
    goto InHand
  HoldAim:
    WEBC D 3
    WEBC D 0 A_JumpIfInventory("IsBot",1,"FireAim")
    WEBC D 1 A_ReFire
    goto Aiming
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("WebleyAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    WEBE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    WEBE A 0 A_WeaponReady(WRF_NOFIRE)
    WEBE A 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE B 0 A_WeaponReady(WRF_NOFIRE)
    WEBE B 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE C 0 A_PlaySoundEx("webley/open","SoundSlot5")
    WEBE C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    WEBE C 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE D 0 A_WeaponReady(WRF_NOFIRE)
    WEBE D 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE E 0 A_WeaponReady(WRF_NOFIRE)
    WEBE E 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE F 0 A_WeaponReady(WRF_NOFIRE)
    WEBE F 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE G 0 A_WeaponReady(WRF_NOFIRE)
    WEBE G 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE H 0 A_PlaySoundEx("webley/clipout","SoundSlot5")
    WEBE H 0 A_WeaponReady(WRF_NOFIRE)
    WEBE H 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE I 0 A_WeaponReady(WRF_NOFIRE)
    WEBE I 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE J 0 A_WeaponReady(WRF_NOFIRE)
    WEBE J 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE K 0 A_WeaponReady(WRF_NOFIRE)
    WEBE K 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE L 0 A_WeaponReady(WRF_NOFIRE)
    WEBE L 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE M 0 A_WeaponReady(WRF_NOFIRE)
    WEBE M 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE N 0 A_WeaponReady(WRF_NOFIRE)
    WEBE N 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE O 0 A_WeaponReady(WRF_NOFIRE)
    WEBE O 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE P 0 A_WeaponReady(WRF_NOFIRE)
    WEBE P 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE Q 0 A_PlaySoundEx("webley/clipin","SoundSlot5")
    WEBE Q 0 A_WeaponReady(WRF_NOFIRE)
    WEBE Q 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE R 0 A_WeaponReady(WRF_NOFIRE)
    WEBE R 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE S 0 A_WeaponReady(WRF_NOFIRE)
    WEBE S 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE T 0 A_WeaponReady(WRF_NOFIRE)
    WEBE T 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE U 0 A_WeaponReady(WRF_NOFIRE)
    WEBE U 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("WebleyClip",1)
    TNT1 A 0 A_TakeInventory("WebleyAmmo",1)
    TNT1 A 0 A_JumpIfInventory("WebleyClip",6,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("WebleyAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    WEBE V 0 A_WeaponReady(WRF_NOFIRE)
    WEBE V 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE W 0 A_WeaponReady(WRF_NOFIRE)
    WEBE W 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE X 0 A_WeaponReady(WRF_NOFIRE)
    WEBE X 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE Y 0 A_WeaponReady(WRF_NOFIRE)
    WEBE Y 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBE Z 0 A_WeaponReady(WRF_NOFIRE)
    WEBE Z 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBF A 0 A_WeaponReady(WRF_NOFIRE)
    WEBF A 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBF B 0 A_PlaySoundEx("webley/close","SoundSlot5")
    WEBF B 0 A_WeaponReady(WRF_NOFIRE)
    WEBF B 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBF C 0 A_WeaponReady(WRF_NOFIRE)
    WEBF C 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    WEBF D 0 A_WeaponReady(WRF_NOFIRE)
    WEBF D 4 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    WEBC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    WEBC A 0 A_ZoomFactor(1.0)		//No FOV change for handguns
    WEBC ABCD 2
    WEBC D 0 A_GiveInventory("Aiming",1)
    WEBC D 0 ACS_ExecuteAlways(602,0,0,0,0)
    WEBC D 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    WEBC D 0 A_ZoomFactor(1.0)
    WEBC DCBA 2
    WEBC A 0 A_TakeInventory("Aiming",1)
    WEBC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    WEBC D 1 A_ZoomFactor(1.0)
    WEBC DCBA 1
    WEBC A 0 A_TakeInventory("Aiming",1)
    WEBC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,1)		//Recoil script
    WEBG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    WEBG BC 3
    WEBG D 3 A_FireCustomMissile("MeleeAttack",0,0,0,0)
    TNT1 AA 3
    WEBG EF 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGE ABCDEF 3
    FRGE G 3 A_PlaySoundEx("grenade/pinpull","SoundSlot5")
    TNT1 A 15 
    FRGE H 3
    FRGE H 0 A_PlaySoundEx("grenade/throw","SoundSlot5")
    FRGE I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGE I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGE I 0 A_TakeInventory("KnifeAttack",1)
    FRGE I 3 A_FireCustomMissile("MillsBomb",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor WebleyEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor WebleyClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 6
    ammo.backpackamount 0
    ammo.backpackmaxamount 6
    +IGNORESKILL
}

actor WebleyPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuff_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("webley/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("webley/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("webley/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("webley/fire4","SoundSlot6")
    stop
  }
}


actor WebleyBullet : HHBulletPuff
{
  damagetype "Webley"
}
