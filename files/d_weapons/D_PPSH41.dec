actor "PPSh-41" : RGUWeapon
{
  obituary "\cf%k \cc>> \cqPPSh-41 \cc>> \cf%o"
  inventory.pickupmessage "PPSh-41"  
  weapon.selectionorder 15
  weapon.ammotype "PPSh41Clip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "PPSh41Ammo"
  weapon.ammouse2 0
  weapon.ammogive2 71
  -NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",3)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    PPHA A 2 A_PlayWeaponSound("weapon/select")
    PPHA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    PPHA E 0 A_JumpIfNoAmmo("Reload")
    PPHA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    PPHA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    PPHA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    PPHA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHA E 1 A_WeaponReady
    loop 
  Aiming:
    PPHC D 0 A_JumpIfInventory("Reloading",1,"Reload")    
    PPHC D 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    PPHC D 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHC D 1 A_WeaponReady(WRF_NOBOB)
    PPHC D 0 A_ZoomFactor(1.2)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    PPHH ABC 2
    TNT1 AA 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    PPHA E 0 A_JumpIfInventory("Aiming",1,"FireAim")
    PPHB A 0 A_JumpIfNoAmmo("ReloadStart")
    PPHB A 0 A_FireCustomMissile("PPS42Puff",0,0,5,0)
    PPHB A 1 BRIGHT A_FireBullets (12.5, 12.5, -1, 45, "PPSh41Bullet", 1, 3000)
    PPHB B 0 ACS_ExecuteAlways(600,0,700,25*random(2,4),0)		//Recoil script
    PPHB B 1 BRIGHT A_GunFlash
    PPHB C 1 A_JumpIfNoAmmo("FireEmpty")
    PPHB A 0 A_JumpIfInventory("PPSh41Ammo",1,2)
    PPHB A 0 A_ReFire
    goto InHand
    PPHB A 0 A_JumpIfNoAmmo(2)
    PPHB A 0 A_ReFire
    goto InHand
    PPHB A 0 A_GiveInventory("PPSh41Empty",1)		//Go to reload
    goto Reload
  FireEmpty:
    PPHB ABC 1 
    PPHB D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+7
  FireAim:
    PPHC D 0 A_JumpIfNoAmmo("ReloadStart")
    PPHD A 0 A_FireCustomMissile("PPS42Puff",0,0,0,5)
    PPHD A 1 BRIGHT A_FireBullets (0.9, 0.9, -1, 45, "PPSh41Bullet", 1, 5000)
    PPHD B 0 ACS_ExecuteAlways(600,0,700,25*random(1,2),0)	//Recoil script
    PPHD B 1 BRIGHT A_GunFlash
    PPHD B 0 A_ZoomFactor(1.18)
    PPHD C 1 A_JumpIfNoAmmo("FireEmptyAim")
    PPHD A 0 A_JumpIfInventory("PPSh41Ammo",1,2)
    PPHD A 0 A_ReFire
    goto Aiming
    PPHD A 0 A_JumpIfNoAmmo(2)
    PPHD A 0 A_ReFire
    goto Aiming
    PPHD A 0 A_GiveInventory("PPSh41Empty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    PPHD ABC 1 
    PPHD D 1 A_PlayWeaponSound("weapon/dryfire")
    goto Fire+7
  Hold:
    PPHA E 0 A_JumpIfInventory("Aiming",1,"HoldAim")
    PPHA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    PPHA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    PPHC D 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("PPSh41Ammo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    PPHE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    PPHE A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHE B 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHE C 0 A_PlaySoundEx("ppsh/clipout","SoundSlot5")
    PPHE C 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)         //This apparent "messy" setup allows for interruption at all times
    PPHE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHE D 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHE E 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHE F 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHE G 0 A_PlaySoundEx("ppsh/clipin","SoundSlot5")
    PPHE G 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHE H 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("PPSh41Clip",1)
    TNT1 A 0 A_TakeInventory("PPSh41Ammo",1)
    TNT1 A 0 A_JumpIfInventory("PPSh41Clip",71,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("PPSh41Ammo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    PPHE I 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_JumpIfInventory("PPSh41Empty",1,"ReloadEndEmpty")
    PPHE J 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHE K 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  ReloadEndEmpty:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_TakeInventory("PPSh41Empty",1)
    PPHF A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHF A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHF B 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHF B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHF C 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHF C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHF D 0 A_PlaySoundEx("ppsh/bolt","SoundSlot5")
    PPHF D 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHF D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHF E 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHF E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    PPHF F 0 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    PPHF F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    PPHC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    PPHC A 0 A_ZoomFactor(1.2)		
    PPHC ABCD 2
    PPHC D 0 A_GiveInventory("Aiming",1)
    PPHC D 0 ACS_ExecuteAlways(602,0,0,0,0)
    PPHC D 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    PPHC D 0 A_ZoomFactor(1.0)
    PPHC DCBA 2
    PPHC A 0 A_TakeInventory("Aiming",1)
    PPHC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    PPHC D 1 A_ZoomFactor(1.0)
    PPHC DCBA 1
    PPHC A 0 A_TakeInventory("Aiming",1)
    PPHC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    PPHG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    PPHG B 3
    PPHG C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    PPHG DEFG 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGD ABCDEFG 3
    TNT1 A 15 
    FRGD H 3
    FRGD H 0 A_PlaySoundEx("stick/throw","SoundSlot5")
    FRGD I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGD I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGD I 0 A_TakeInventory("KnifeAttack",1)
    FRGD I 3 A_FireCustomMissile("RussianGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  }
}

actor PPSh41Empty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor PPSh41Clip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 71
    ammo.backpackamount 0
    ammo.backpackmaxamount 71
    +IGNORESKILL
}


actor PPSh41Bullet : HHBulletPuff
{
  damagetype "PPSh41"
}
