actor "Mosin-Nagant" : RGUWeapon
{
  obituary "\cf%k \cc>> \csMosin-Nagant \cc>> \cf%o"
  inventory.pickupmessage "Mosin-Nagant"  
  weapon.selectionorder 19
  weapon.ammotype "NagantClip"
  weapon.ammouse 1
  weapon.ammogive 1
  weapon.ammotype2 "NagantAmmo"
  weapon.ammouse2 0
  weapon.ammogive2 5
  +NOAUTOFIRE
  states
  {
  Select:
    TNT1 A 0 A_Raise
    loop
  Ready:
    TNT1 A 0 A_GiveInventory("Weight",3)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    NAGA A 2 A_PlayWeaponSound("weapon/select")
    NAGA BCDE 2 A_WeaponReady(WRF_NOPRIMARY)
    //goto InHand 				-- intentional drop to next state
  InHand: 					//Ready state
    NAGA E 0 A_JumpIfNoAmmo("Reload")
    NAGA E 0 A_JumpIfInventory("Aiming",1,"Aiming")
    NAGA E 0 A_JumpIfInventory("Reloading",1,"Reload")
    NAGA E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    NAGA E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGA E 1 A_WeaponReady
    loop 
  Aiming:
    NAGC E 0 A_JumpIfInventory("Reloading",1,"Reload")    
    NAGC E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    NAGC E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGC E 1 A_WeaponReady(WRF_NOBOB)
    loop
  Deselect:
    TNT1 A 0 A_TakeInventory("Weight",5)	//Determines player speed - 3 means 20% less speed
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 A_ZoomFactor(1.0)
    NAGH ABCD 2
    TNT1 A 2
  DeselectFull: 				//Deselect
    TNT1 AA 0 A_Lower
    TNT1 A 1 A_Lower
    loop
  Fire:
    NAGA E 1 A_JumpIfInventory("Aiming",1,"FireAim")
    NAGA E 1 A_JumpIfInventory("NagantBolt",1,"Bolt")
    NAGB A 0 A_JumpIfNoAmmo("ReloadStart")
    NAGB A 0 A_TakeInventory("NagantClosed",1)
    NAGB A 0 A_FireCustomMissile("NagantPuff",0,0,4,2)
    NAGB A 1 BRIGHT A_FireBullets (10, 10, -1, 100, "NagantBullet3", 1, 3000)
    NAGB B 0 ACS_ExecuteAlways(600,0,1500,150*random(2,4),0)		//Recoil script
    NAGB B 1 BRIGHT A_GunFlash
    NAGB CDEE 1 A_JumpIfNoAmmo("FireEmpty")
    NAGB E 5 A_WeaponReady(WRF_NOFIRE)
    NAGB E 0 A_GiveInventory("NagantBolt",1)
    NAGA E 2 A_WeaponReady(WRF_NOFIRE)
    goto InHand
    NAGB A 0 A_JumpIfNoAmmo(2)
    goto InHand
    NAGB A 0 A_GiveInventory("NagantEmpty",1)		//Go to reload
    goto Reload
  FireEmpty:
    NAGB CDEF 1
    goto Fire+14
  FireAim:
    NAGA E 0 A_JumpIfInventory("NagantBolt",1,"Bolt")  
    NAGC E 1 A_JumpIfNoAmmo("ReloadStart")
    NAGD A 0 A_TakeInventory("NagantClosed",1)
    NAGD A 0 A_FireCustomMissile("NagantPuff",0,0,0,4)
    NAGD A 1 BRIGHT A_FireBullets (0.35, 0.35, -1, 100, "NagantBullet", 1, 9001)
    NAGD B 0 ACS_ExecuteAlways(600,0,1500,150*random(1,2),0)		//Recoil script
    NAGD B 1 BRIGHT A_GunFlash
    NAGD B 1 A_ZoomFactor(1.18)
    NAGD CDEE 1 A_JumpIfNoAmmo("FireEmptyAim")
    NAGD E 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    NAGD E 1 A_ZoomFactor(1.2)
    NAGD E 0 A_GiveInventory("NagantBolt",1)
    NAGD A 0 A_JumpIfInventory("NagantAmmo",1,2)
    NAGD A 0 A_ReFire
    goto InHand
    NAGD A 0 A_JumpIfNoAmmo(2)
    goto InHand
    NAGD A 0 A_GiveInventory("NagantEmpty",1)		//Go to reload
    goto Reload
  FireEmptyAim:
    NAGD CDEF 1
    NAGD F 0 A_TakeInventory("Aiming",1)
    goto Fire+14
  Hold:
    NAGE E 0 A_JumpIfInventory("Aiming",1,"HoldAim")	//Don't need a hold because of the already long rechamber time
    NAGE E 0 A_JumpIfInventory("ThrowingGrenade",1,"KnifeAttack")
    NAGE E 0 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto Fire
  HoldAim:
    NAGC E 0
    goto FireAim
  Flash:
    TNT1 A 2 bright A_Light1
    TNT1 A 0 bright A_Light0
    stop
  Reload:
    TNT1 A 0 A_JumpIfInventory("NagantAmmo",1,"ReloadStart")
    TNT1 A 0 A_TakeInventory("Reloading",1)
    goto InHand+3
  ReloadStart:
    NAGE A 0 A_JumpIfInventory("Aiming",1,"ReloadStartAim")
    NAGE A 0 A_WeaponReady(WRF_NOFIRE)
    NAGE A 0 A_ZoomFactor(1.0)
    NAGE A 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE B 0 A_TakeInventory("NagantClosed",1)
    NAGE B 0 A_PlaySoundEx("nagant/open","SoundSlot5")
    NAGE B 0 A_WeaponReady(WRF_NOFIRE)
    NAGE B 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE C 0 A_WeaponReady(WRF_NOFIRE)				//This apparent "messy" setup allows for interruption at all times
    NAGE C 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE D 0 A_WeaponReady(WRF_NOFIRE)
    NAGE D 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE E 0 A_WeaponReady(WRF_NOFIRE)
    NAGE E 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE F 0 A_WeaponReady(WRF_NOFIRE)
    NAGE F 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE G 0 A_WeaponReady(WRF_NOFIRE)
    NAGE G 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE H 0 A_WeaponReady(WRF_NOFIRE)
    NAGE H 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE I 0 A_WeaponReady(WRF_NOFIRE)
    NAGE I 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE J 0 A_PlaySoundEx("nagant/clipin","SoundSlot5")
    NAGE J 0 A_WeaponReady(WRF_NOFIRE)
    NAGE J 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE K 0 A_WeaponReady(WRF_NOFIRE)
    NAGE K 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    goto ReloadAmmo
  ReloadAmmo:
    TNT1 A 0 A_GiveInventory("NagantClip",1)
    TNT1 A 0 A_TakeInventory("NagantAmmo",1)
    TNT1 A 0 A_TakeInventory("NagantBolt",1)
    TNT1 A 0 A_JumpIfInventory("NagantClip",5,"ReloadEnd")
    TNT1 A 0 A_JumpIfInventory("NagantAmmo",1,"ReloadAmmo")
    goto ReloadEnd
  ReloadEnd:
    TNT1 A 0 A_TakeInventory("Reloading",1)
    NAGE L 0 A_GiveInventory("NagantClosed",1)
    NAGE L 0 A_PlaySoundEx("nagant/close","SoundSlot5")
    NAGE L 0 A_WeaponReady(WRF_NOFIRE)
    NAGE L 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE M 0 A_WeaponReady(WRF_NOFIRE)
    NAGE M 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    NAGE N 0 A_WeaponReady(WRF_NOFIRE)
    NAGE N 5 A_JumpIfInventory("KnifeAttack",1,"KnifeAttack")
    TNT1 A 0 A_ReFire
    goto Ready+12
  AltFire:	//Aim
    NAGC A 0 A_JumpIfInventory("Aiming",1,"AltFire2")
    NAGC A 0 A_ZoomFactor(1.2)		
    NAGC ABCDE 2
    NAGC E 0 A_GiveInventory("Aiming",1)
    NAGC E 0 ACS_ExecuteAlways(602,0,0,0,0)
    NAGC E 0 ACS_ExecuteAlways(600,0,1,0,3)
    goto Aiming
  AltFire2:
    NAGC E 0 A_ZoomFactor(1.0)
    NAGC EDCBA 2
    NAGC A 0 A_TakeInventory("Aiming",1)
    NAGC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto InHand
  ReloadStartAim:
    NAGC E 1 A_ZoomFactor(1.0)
    NAGC EDCBA 1
    NAGC A 0 A_TakeInventory("Aiming",1)
    NAGC A 0 ACS_ExecuteAlways(602,0,0,0,0)
    goto ReloadStart+1
  Spawn:
    WEAP A -1
    loop
  KnifeAttack: //Butt of weapon
    TNT1 A 0 A_ZoomFactor(1.0)
    TNT1 A 0 A_TakeInventory("Aiming",1)
    TNT1 A 0 A_TakeInventory("Reloading",1)
    TNT1 A 0 ACS_ExecuteAlways(602,0,0,0,0)
    TNT1 A 0 A_JumpIfInventory("ThrowingGrenade",1,"FragGrenade")
    TNT1 A 0 ACS_ExecuteAlways(600,0,900,900,2)		//Recoil script
    NAGG A 3 A_PlaySoundEx("melee/swing","SoundSlot5")
    NAGG B 3
    NAGG C 3 A_FireCustomMissile("MeleeAttack",4,0,0,-4)
    NAGG DEFG 3
    NAGA B 3
    TNT1 A 0 A_TakeInventory("KnifeAttack",1)
    goto InHand
  FragGrenade:
    FRGD ABCDEFG 3
    TNT1 A 15 
    FRGD H 3
    FRGD H 0 A_PlaySoundEx("stick/throw","SoundSlot5")
    FRGD I 0 A_TakeInventory("FragGrenadeAmmo",1)
    FRGD I 0 A_TakeInventory("ThrowingGrenade",1)
    FRGD I 0 A_TakeInventory("KnifeAttack",1)
    FRGD I 3 A_FireCustomMissile("RussianGrenade",0,0,0,16)
    goto Ready+1		//Reselect weapon
  Bolt:
    NAGB H 0 A_JumpIfInventory("Aiming",1,"BoltAim")
    NAGB H 0 A_GiveInventory("NagantClosed",1)
    NAGB H 0 A_TakeInventory("NagantBolt",1)
    NAGB H 5 A_PlayWeaponSound("kar98k/chamber")
    NAGB IJKL 5 A_WeaponReady(WRF_NOFIRE)
    goto InHand
  BoltAim:
    NAGD H 0 A_ZoomFactor(1.0)
    NAGD H 0 A_GiveInventory("NagantClosed",1)
    NAGD H 0 A_TakeInventory("NagantBolt",1)
    NAGD H 5 A_PlayWeaponSound("kar98k/chamber")
    NAGD IJKL 5 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
    NAGD L 0 A_ZoomFactor(1.2)
    goto InHand
  }
}

actor NagantEmpty : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor NagantClosed : Inventory
{
    inventory.amount 0
    inventory.maxamount 1
}
actor NagantClip : Ammo
{
    inventory.icon "CLIPA0"
    inventory.amount 0
    inventory.maxamount 5
    ammo.backpackamount 0
    ammo.backpackmaxamount 5
    +IGNORESKILL
}

actor NagantPuff
{
  RenderStyle None
  -RANDOMIZE
  +NOCLIP
  PROJECTILE
  Speed 28
  Damage 0
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_SpawnItemEx("WeaponSmokePuffShotgun_Spawned", 1, 0, 0, 0, 0, 0, 0, 128, 0)
    TNT1 A 1 A_SpawnItemEx("MuzzleFlash2", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 0 A_Jump(128,"Spawn2")
    TNT1 A 0 A_PlaySoundEx("Nagant/fire1","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("Nagant/fire3","SoundSlot6")
    stop
  Spawn2:
    TNT1 A 0 A_PlaySoundEx("Nagant/fire2","SoundSlot5")
    TNT1 A 5 A_PlaySoundEx("Nagant/fire4","SoundSlot6")
    stop
  }
}

actor NagantBullet : HHBulletPuff
{
  damagetype "Nagant"
}
actor NagantBullet3 : HHBulletPuff
{
  damagetype "Nagant3"
}

actor NagantBolt : Inventory //If this is in the the players inventory the rifle needs to be bolted.
{
    inventory.amount 0
    inventory.maxamount 1
}
